# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Sergio Padrino <sergio.padrino@gmail.com>

pkgname=gamecube-sdl
pkgver=1.2.15
pkgrel=6
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('gamecube-pkg-config' 'dkp-toolchain-vars')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch" "config.site")
groups=('gamecube-portlibs' 'gamecube-sdl-libs')
sha256sums=(
  'd6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
  '32aa0c594b6ecbb1f4a0842d05a5c2274e1dd0ba38cdd6e6d2f250ed610a37ff'
  'eb33978edaeeb22f83d108ea5236e7f749c3fb3d60765e69f624af2365e2b854'
)

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/cubevars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  CONFIG_SITE=${srcdir}/config.site ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-nintendo-gamecube
  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/cubevars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

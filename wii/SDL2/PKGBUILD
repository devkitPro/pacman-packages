# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wii-sdl2
pkgver=2.28.5
pkgrel=15
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('wii-pkg-config' 'dkp-toolchain-vars')
depends=('wii-opengx' 'libogc')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('wii-portlibs' 'wii-sdl2-libs')

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/wiivars.sh

  patch -Np1 -i $srcdir/SDL2-${pkgver}.patch

  /opt/devkitpro/portlibs/wii/bin/powerpc-eabi-cmake -S. -B _build -DCMAKE_BUILD_TYPE=Release -G Ninja

  cmake --build _build --parallel
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/wiivars.sh

  DESTDIR=${pkgdir} cmake --install _build

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

sha256sums=('332cb37d0be20cb9541739c61f79bae5a477427d79ae85e352089afdaf6666e4'
            'd89771e8e2d5f2b1d01af8129f5efb4e1e65016cea7d8dfbe2b76278bc6c7c69')

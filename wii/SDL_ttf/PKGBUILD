# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wii-sdl_ttf
pkgver=2.0.11
pkgrel=4
pkgdesc="A sample library which allows you to use TrueType fonts in your SDL applications"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=(
 'ppc-freetype'
 'wii-sdl'
)
makedepends=(
 'wii-pkg-config'
 'dkp-toolchain-vars'
)
source=("${url}/projects/SDL_ttf/release/SDL_ttf-${pkgver}.tar.gz")
groups=('wii-portlibs' 'wii-sdl-libs')
sha256sums=(
'724cd895ecf4da319a3ef164892b72078bd92632a5d812111261cde248ebcdb7'
)

build() {
  cd SDL_ttf-$pkgver

  source /opt/devkitpro/wiivars.sh

  sed '/^noinst_PROGRAMS/d' -i Makefile.in
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd SDL_ttf-$pkgver

  source /opt/devkitpro/wiivars.sh

  make DESTDIR="$pkgdir" install

}

# Maintainer: Dave Murphy <davem@devkitpro.org>

_realname=newlib

pkgname=devkitppc-${_realname}

pkgver=4.5.0.20241231
pkgrel=2
pkgdesc='devkitPPC newlib'

arch=('any')

_target=powerpc-eabi

export _TOOLPATH=${TOOLPATH:-/opt/devkitpro/devkitPPC}

source=(
    "https://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz"
    "https://pkg.devkitpro.org/patches/devkitppc-newlib-${pkgver}.patch"
)

options=(!strip)

prepare() {
  patch -p1 -d ${srcdir}/newlib-${pkgver} -i ${srcdir}/devkitppc-newlib-${pkgver}.patch
}

build() {

  export PATH=${_TOOLPATH}/bin:${PATH}

  mkdir -p ${srcdir}/_build_newlib
  cd ${srcdir}/_build_newlib

  CFLAGS_FOR_TARGET="-O2 -ffunction-sections -fdata-sections -DCUSTOM_MALLOC_LOCK" \
  ${srcdir}/newlib-${pkgver}/configure \
    --target=${_target} \
    --prefix=/opt/devkitpro/devkitPPC/ \
    --enable-newlib-mb \
    --enable-newlib-register-fini \
    --disable-newlib-wide-orient \

  make

}

package() {

  cd ${srcdir}/_build_newlib
  make DESTDIR="$pkgdir" install -j1

  find ${pkgdir}/opt/devkitpro/devkitPPC/${_target}/lib \( -name "*.a" -or -name "*.o" \) -exec ${_target}-objcopy -R .comment -R .note -R .debug_info -R .debug_aranges -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;


}

sha256sums=('33f12605e0054965996c25c1382b3e463b0af91799001f5bb8c0630f2ec8c852'
            '1ad5b49e3f45d0b8e82153c6434e6a6d22132baab71b6425a282e08943a0e34c')

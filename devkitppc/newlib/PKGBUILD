# Maintainer: Dave Murphy <davem@devkitpro.org>

_realname=newlib

pkgname=devkitppc-${_realname}

pkgver=4.6.0.20260123
pkgrel=2
pkgdesc='devkitPPC newlib'

arch=('any')

_target=powerpc-eabi

export _TOOLPATH=${TOOLPATH:-/opt/devkitpro/devkitPPC}

source=(
    "https://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz"
    "https://raw.githubusercontent.com/devkitPro/buildscripts/refs/tags/devkitPPC_r49.1/patches/newlib-${pkgver}-${pkgrel}.patch"
)

options=(!strip)

prepare() {
  patch -p1 -d ${srcdir}/newlib-${pkgver} -i ${srcdir}/newlib-${pkgver}-${pkgrel}.patch
  touch ${srcdir}/newlib-${pkgver}/libgloss/doc/porting.info
}

build() {

  export PATH=${_TOOLPATH}/bin:${PATH}

  mkdir -p ${srcdir}/_build_newlib
  cd ${srcdir}/_build_newlib

  CFLAGS_FOR_TARGET="-O2 -ffunction-sections -fdata-sections -DCUSTOM_MALLOC_LOCK" \
  ${srcdir}/newlib-${pkgver}/configure \
    --target=${_target} \
    --prefix=/opt/devkitpro/devkitPPC/ \
    --enable-newlib-mb \
    --enable-newlib-register-fini \
    --disable-newlib-wide-orient \

  make

}

package() {

  cd ${srcdir}/_build_newlib
  make DESTDIR="$pkgdir" install -j1

  find ${pkgdir}/opt/devkitpro/devkitPPC/${_target}/lib \( -name "*.a" -or -name "*.o" \) -exec ${_target}-objcopy -R .comment -R .note -R .debug_info -R .debug_aranges -R .debug_pubnames -R .debug_pubtypes -R .debug_abbrev -R .debug_line -R .debug_str -R .debug_ranges -R .debug_loc '{}' \;


}

sha256sums=('6ff27e3bf022666f43f7802255be680eeff722ac181b1725d21e2e8318604ee3'
            'a34a75651e573c7bd8ac5c0c26cca79403fedb78f06d7c7ec665b7452f71598c')

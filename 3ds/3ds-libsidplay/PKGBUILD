# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: badda71 <me@badda.de>

pkgname=3ds-libsidplay
pkgver=1.36.60
pkgrel=1.0
pkgdesc="SID chip music module playing library"
arch=('any')
url="https://github.com/badda71/3ds-libsidplay"
license=('GPLv2')
options=(!strip libtool staticlibs)
source=(
	"http://ftp.jaist.ac.jp/pub/Linux/Momonga/development/source/SOURCES/libsidplay-${pkgver}.tar.bz2"
	"libsidplay-${pkgver}.patch"
)
sha256sums=('1729beb5b54e7486e0b2b83dc0e02130e753a8dcd6fe03677217dbe358941396'
            '202ea2380a2a3e6e6a4fb533f304fe5b7669a1e1fb0e76a25a20f00bb9091dda')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('3ds-portlibs')

build() {
  cd libsidplay-$pkgver
  patch -p0 -i ../libsidplay-${pkgver}.patch
  source /opt/devkitpro/3dsvars.sh
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libsidplay-$pkgver
  source /opt/devkitpro/3dsvars.sh
  make DESTDIR="$pkgdir" install
}

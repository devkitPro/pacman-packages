# Maintainer: Dave Murphy <davem@devkitpro.org>
_realname=physfs
pkgname=3ds-${_realname}
pkgver=3.2.0
pkgrel=1
pkgdesc='A library to provide abstract access to various archives.'
arch=('any')
url='https://icculus.org/physfs/'
license=('zlib')
options=(!strip libtool staticlibs)
source=( "${_realname}-${pkgver}.tar.gz::https://github.com/icculus/physfs/archive/refs/tags/release-${pkgver}.tar.gz"
         "physfs-${pkgver}.patch")
makedepends=('devkitARM' 'dkp-toolchain-vars' '3ds-cmake')
sha256sums=(
  '1991500eaeb8d5325e3a8361847ff3bf8e03ec89252b7915e1f25b3f8ab5d560'
  '97bdab0dc338921fe43d67b28225363fc3552c89b11fb28f3ff5889da5ab9d1c'
)
groups=('3ds-portlibs')
build() {
  cd physfs-release-$pkgver

  patch -Np1 -i "$srcdir"/physfs-${pkgver}.patch

  source ${DEVKITPRO}/3dsvars.sh

  arm-none-eabi-cmake  -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/3ds \
    -DPHYSFS_BUILD_STATIC=ON -DPHYSFS_BUILD_SHARED=OFF \
    -DPHYSFS_BUILD_TEST=OFF \
    .

  make
}

package() {
  cd physfs-release-$pkgver

  make install DESTDIR="$pkgdir"
}

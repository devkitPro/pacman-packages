# Maintainer: WinterMute <davem@devkitpro.org>
# Contributor: Elouan Martinet <exa@elou.world>

pkgname=3ds-curl
pkgver=7.59.0
pkgrel=2
pkgdesc='Library for transferring data with URLs. (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://curl.haxx.se'
license=('MIT')
options=(!strip libtool staticlibs)
depends=('3ds-zlib' '3ds-mbedtls')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')

source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"
        '3ds-curl.patch'
)

sha256sums=('099d9c32dc7b8958ca592597c9fabccdf4c08cfb7c114ff1afbbc4c6f13c9e9e'
            '937dc6fc621ce29cb14bf1083b232dd1c795618ad2a5c4b53ad399be189ef873')

build() {
  cd curl-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  patch -Np1 -i $srcdir/3ds-curl.patch

  ./configure CFLAGS="${CFLAGS}" CPPFLAGS="${CPPFLAGS}" LIBS="-lctru" \
    --prefix=${PORTLIBS_PREFIX} --host=arm-none-eabi \
    --disable-shared --enable-static --disable-ipv6 --disable-unix-sockets \
    --disable-manual --disable-ntlm-wb --disable-threaded-resolver \
    --with-mbedtls=$PORTLIBS_PREFIX

  make -C lib
}

package() {
  cd curl-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" -C lib install
  make DESTDIR="$pkgdir" -C include install

  install -Dm644 COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libopus
pkgver=1.3
pkgrel=1
pkgdesc='Reference implementation of the lossy audio codec, Opus'
arch=('any')
url='https://opus-codec.org/'
license=(BSD)
options=(!strip libtool staticlibs)
source=("https://archive.mozilla.org/pub/opus/opus-$pkgver.tar.gz")
sha256sums=('4f3d69aefdf2dbaf9825408e452a8a414ffc60494c70633560700398820dc550')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')

build() {
  cd opus-$pkgver

  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd opus-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
}

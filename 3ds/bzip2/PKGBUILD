
# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgname=('3ds-bzip2')
pkgver=1.0.6
pkgrel=3
pkgdesc="A high-quality data compression program"
arch=('any')
license=('custom')
url="http://www.bzip.org"
options=(!strip libtool staticlibs)
source=(http://http.debian.net/debian/pool/main/b/bzip2/bzip2_$pkgver.orig.tar.bz2)
sha256sums=('d70a9ccd8bdf47e302d96c69fecd54925f45d9c7b966bb4ef5f56b770960afa7')
makedepends=('devkitARM' 'devkitarm-rules' 'devkitarm-crtls' '3ds-pkg-config' 'dkp-toolchain-vars')
groups=('3ds-portlibs')

build() {

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make -C "$srcdir/bzip2-$pkgver" \
  CC=arm-none-eabi-gcc \
  AR=arm-none-eabi-ar \
  RANLIB=arm-none-eabi-ranlib \
  CPPFLAGS="${CPPFLAGS}" \
  CFLAGS="-D_FILE_OFFSET_BITS=64 -Winline ${CFLAGS}" libbz2.a

}

package() {
  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  install -Dm 644 "$srcdir/bzip2-$pkgver"/LICENSE -t "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/bzlib.h -t "${pkgdir}/${PORTLIBS_PREFIX}/include/"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/libbz2.a -t "${pkgdir}/${PORTLIBS_PREFIX}/lib"
}

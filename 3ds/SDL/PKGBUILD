# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-sdl
pkgver=1.2.15
pkgrel=18
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo 3DS port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch")
groups=('3ds-portlibs' '3ds-sdl-libs')


prepare() {
  cd SDL-$pkgver

  source /opt/devkitpro/3dsvars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static --enable-n3ds

}

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
sha256sums=('d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
            'f55bfab26f5210c6d0c5cdec2ac630f11ed33239d7bd864b6dae88705af0a33f')

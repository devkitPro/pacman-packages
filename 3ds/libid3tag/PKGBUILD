
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=3ds-libid3tag
pkgver=0.15.1b
pkgrel=2
pkgdesc='A library for reading and (eventually) writing ID3 tags, both ID3v1 and the various versions of ID3v2. (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://www.underbit.com'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://sourceforge.net/projects/mad/files/libid3tag/0.15.1b/libid3tag-${pkgver}.tar.gz")
groups=('3ds-portlibs')
sha256sums=('63da4f6e7997278f8a3fef4c6a372d342f705051d1eeb6a46a86b03610e26151')
depends=('3ds-zlib')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')

build() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  CPPFLAGS="${CFLAGS} ${CPPFLAGS}" ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
}

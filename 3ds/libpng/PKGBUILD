# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=3ds-libpng
pkgver=1.6.53
pkgrel=1
pkgdesc='PNG format graphic files library'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('3ds-zlib')
groups=('3ds-portlibs')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-tools --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make LN_S=cp DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}
sha256sums=('1d3fb8ccc2932d04aa3663e22ef5ef490244370f4e568d7850165068778d98d4')

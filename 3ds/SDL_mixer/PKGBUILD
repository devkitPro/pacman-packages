# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-sdl_mixer
pkgver=1.2.12
pkgrel=9
pkgdesc="A simple multi-channel audio mixer library (Nintendo 3DS port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
depends=(
 '3ds-sdl'
 '3ds-libvorbisidec'
 '3ds-mikmod'
 '3ds-libogg'
 '3ds-libmad'
)
makedepends=(
 '3ds-pkg-config'
 'devkitpro-pkgbuild-helpers'
)
source=("${url}/projects/SDL_mixer/release/SDL_mixer-${pkgver}.tar.gz")
sha256sums=(
'1644308279a975799049e4826af2cfc787cad2abb11aa14562e402521f86992a'
)
groups=('3ds-portlibs' '3ds-sdl-libs')

build() {
  cd SDL_mixer-$pkgver

  source /opt/devkitpro/3dsvars.sh

  sed -i 's,\$(objects)/playwave\$(EXE) \$(objects)/playmus\$(EXE),,g' Makefile.in

  LIBS="-logg" ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static \
    --enable-music-ogg-tremor \
    --disable-music-cmd \
    --enable-music-mp3-mad-gpl

  make
}

package() {
  cd SDL_mixer-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: vorbisidec ogg libmikmod" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL_mixer.pc"
  sed -i 's,-lSDL_mixer,-lSDL_mixer -lmad,' "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL_mixer.pc"
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-opusfile
pkgver=0.11
pkgrel=1
pkgdesc='Library for opening, seeking, and decoding .opus files'
arch=('any')
url='https://opus-codec.org/'
license=(BSD)
options=(!strip libtool staticlibs)
source=("https://downloads.xiph.org/releases/opus/opusfile-$pkgver.tar.gz")
sha256sums=('74ce9b6cf4da103133e7b5c95df810ceb7195471e1162ed57af415fabf5603bf')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
depends=('3ds-libogg' '3ds-libopus')
groups=('3ds-portlibs')

build() {
  cd opusfile-$pkgver

  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd opusfile-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libconfig
pkgver=1.7.2
pkgrel=1
pkgdesc='C/C++ Configuration File Library (for Nintendo 3DS homebrew development)'
arch=('any')
url="https://www.hyperrealm.com/libconfig/libconfig.html"
license=('LGPL')
options=(!strip libtool staticlibs)
source=("https://hyperrealm.github.io/libconfig/dist/libconfig-${pkgver}.tar.gz")
sha256sums=('7c3c7a9c73ff3302084386e96f903eb62ce06953bb1666235fac74363a16fad9')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')

build() {
  cd libconfig-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
     --disable-shared --enable-static \
     --disable-examples

  make
}

package() {
  cd libconfig-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 COPYING.LIB "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING.LIB
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}


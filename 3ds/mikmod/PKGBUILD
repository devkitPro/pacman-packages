# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-mikmod
pkgver=3.3.11.1
pkgrel=2
pkgdesc="Amodule player and library supporting many formats, including mod, s3m, it, and xm (Nintendo 3DS port)"
arch=('any')
url="http://mikmod.sourceforge.net/"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=(
 '3ds-pkg-config'
 'dkp-toolchain-vars'
)
source=("https://sourceforge.net/projects/mikmod/files/libmikmod/$pkgver/libmikmod-$pkgver.tar.gz")
sha256sums=(
'ad9d64dfc8f83684876419ea7cd4ff4a41d8bcd8c23ef37ecb3a200a16b46d19'
)
groups=('3ds-portlibs')
build() {
  cd libmikmod-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd libmikmod-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install

  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share
}

# Maintainer: WinterMute <davem@devkitpro.org>
# Contributor: Elouan Martinet <exa@elou.world>

pkgname=3ds-mbedtls
pkgver=2.16.6
pkgrel=1
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://tls.mbed.org/'
license=('apache')
options=(!strip libtool staticlibs)
depends=(3ds-zlib)
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
source=( "https://tls.mbed.org/download/mbedtls-${pkgver}-apache.tgz"
         '3ds-mbedtls.patch' )

sha256sums=('66455e23a6190a30142cdc1113f7418158839331a9d8e6b0778631d077281770'
            '3bc52b6a6701ee82342bf39ad2f447b6a7dda7fcfdecb083d909d498e51b5be4')

build() {
  cd mbedtls-$pkgver

  patch -Np1 -i $srcdir/3ds-mbedtls.patch

  ./scripts/config.pl set MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY
  ./scripts/config.pl set MBEDTLS_CMAC_C

  ./scripts/config.pl unset MBEDTLS_SELF_TEST
  ./scripts/config.pl unset MBEDTLS_TIMING_C

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/devkitarm.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="CFLAGS -fno-exceptions -fno-rtti" \
    -DZLIB_ROOT="$PORTLIBS_PREFIX" \
    -DENABLE_ZLIB_SUPPORT=TRUE -DENABLE_TESTING=FALSE -DENABLE_PROGRAMS=FALSE \
    .

  make

}

package() {

  cd mbedtls-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make install DESTDIR="$pkgdir"

  install -d "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname
  cp -v LICENSE "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname 

}

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Sebastian Weber <me@badda.de>

pkgname=3ds-libsidplayfp
pkgver=1.8.8
pkgrel=1
pkgdesc="Library to play Commodore 64 music files (SID-files) derived from libsidplay2"
arch=('any')
url="https://sourceforge.net/projects/sidplay-residfp/"
license=('GPLv2')
options=(!strip libtool staticlibs)
source=(
	"https://netcologne.dl.sourceforge.net/project/sidplay-residfp/libsidplayfp/1.8/libsidplayfp-1.8.8.tar.gz"
)
sha256sums=('d28b0c28ceea62627c137b4194e8bed866c8f2cec65bac97fde63639250a31e0')
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('3ds-portlibs')

build() {
  cd libsidplayfp-$pkgver
  source /opt/devkitpro/3dsvars.sh
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libsidplayfp-$pkgver
  source /opt/devkitpro/3dsvars.sh
  make DESTDIR="$pkgdir" install
}

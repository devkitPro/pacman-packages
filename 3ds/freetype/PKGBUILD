# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgname="3ds-freetype"
pkgver=2.9
pkgrel=1
pkgdesc="TrueType font rendering library (mingw-w64)"
arch=('any')
url="https://www.freetype.org/"
license=(GPL2+ custom:FreeType)
depends=("3ds-bzip2"
         "3ds-libpng"
         "3ds-zlib")
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
options=('!strip' '!libtool' 'staticlibs')
source=(https://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2)
sha256sums=('e6ffba3c8cef93f557d1f767d7bc3dee860ac7a3aaff588a521e081bc36f4c8a')


build() {

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  cd freetype-${pkgver}

  ./configure \
    --host=arm-none-eabi \
    --target=arm-none-eabi \
    --prefix=${PORTLIBS_PREFIX} \
    --enable-static \
    --disable-shared \
    --with-zlib \
    --with-bzip2 \
    --with-png

  make
}

package () {

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  cd freetype-${pkgver}

  make DESTDIR="${pkgdir}" install

  # Licenses
  install -Dm644 "docs/LICENSE.TXT" "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"
}


# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=3ds-libjpeg-turbo
pkgver=1.5.3
pkgrel=2
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://sourceforge.net/projects/libjpeg-turbo'
license=('IJG')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/packages/sources/libjpeg-turbo-$pkgver.tar.xz")
sha256sums=('7c650554cc7742db41ebb014e183ee69d5a1b2f010fbf8a4de11cf783319a42c')
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
groups=('3ds-portlibs')

build() {
  cd libjpeg-turbo-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

    ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libjpeg-turbo-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  cp -v LICENSE.md "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

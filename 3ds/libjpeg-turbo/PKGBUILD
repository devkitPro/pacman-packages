# Maintainer: WinterMute <davem@devkitpro.org>
_libname=libjpeg-turbo
pkgname=3ds-${_libname}
pkgver=3.1.3
pkgrel=1
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression'
arch=('any')
url='https://github.com/libjpeg-turbo/libjpeg-turbo'
license=('IJG')
options=(!strip libtool staticlibs)
source=(
  "${_libname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz"
)
sha256sums=('3a13a5ba767dc8264bc40b185e41368a80d5d5f945944d1dbaa4b2fb0099f4e5')
makedepends=('3ds-cmake' '3ds-pkg-config' 'dkp-toolchain-vars')
groups=('3ds-portlibs')

build() {
  cd libjpeg-turbo-$pkgver

  source /opt/devkitpro/3dsvars.sh

  arm-none-eabi-cmake -DENABLE_SHARED:BOOLEAN=false -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX .

  make
}

package() {
  cd libjpeg-turbo-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  cp -v LICENSE.md "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/bin

}

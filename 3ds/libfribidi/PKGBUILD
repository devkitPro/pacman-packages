# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <dev f4ke de>

pkgname=3ds-libfribidi
pkgver=1.0.12
pkgrel=1
pkgdesc='Free Implementation of the Unicode Bidirectional Algorithm (Nintendo 3DS port)'
arch=('any')
url="https://github.com/fribidi/fribidi"
license=('LGPL2.1')
options=(!strip staticlibs)
makedepends=('3ds-pkg-config' 'dkp-toolchain-vars')
source=("https://github.com/fribidi/fribidi/releases/download/v$pkgver/fribidi-$pkgver.tar.xz")
sha256sums=('0cd233f97fc8c67bb3ac27ce8440def5d3ffacf516765b91c2cc654498293495')
groups=('3ds-portlibs')

prepare() {
  cd fribidi-$pkgver
  # patch out binaries, as they conflict with getopt and do not work anyway
  sed '/^SUBDIRS/ s/bin //' -i Makefile.am
  ./autogen.sh
}

build() {
  cd fribidi-$pkgver

  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="$PORTLIBS_PREFIX" --host=arm-none-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd fribidi-$pkgver

  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install

  # remove documentation
  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share
}

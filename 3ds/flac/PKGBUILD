# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>
# Contributor: oreo639 <oreo6391@gmail.com>

pkgname=3ds-flac
pkgver=1.3.2
pkgrel=1
pkgdesc='Open source Free Lossless Audio Codec'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
depends=("3ds-libogg")
makedepends=('devkitpro-pkgbuild-helpers')
groups=('3ds-portlibs')

source=(
  "https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz"
  "flac-${pkgver}.patch"
)
sha256sums=(
  '91cfc3ed61dc40f47f050a109b08610667d73477af6ef36dcad31c31a4a8d53f'
  'fead32db2d1b74f994746922a71c1a770a0c321f50ff4c4c0e23cafd65f0ceea'
)

build() {
  cd flac-$pkgver

  source /opt/devkitpro/3dsvars.sh

  patch -Np1 -i "$srcdir/flac-${pkgver}.patch"

  CFLAGS="${CFLAGS} -D_3DS " \
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static --disable-examples \
    --disable-xmms-plugin --disable-doxygen-docs \
    --disable-sse --enable-debug

  make -C src/libFLAC
}

package() {
  cd flac-$pkgver

  make DESTDIR="$pkgdir" install
  rm -r $pkgdir/opt/devkitpro/portlibs/3ds/bin
}


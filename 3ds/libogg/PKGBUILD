# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libogg
pkgver=1.3.3
pkgrel=1
pkgdesc='The Ogg transport bitstream is designed to provide framing, error protection and seeking structure for higher-level codec streams that consist of raw, unencapsulated data packets, such as the Opus, Vorbis and FLAC audio codecs or the Theora and Dirac video codecs.'
arch=('any')
url='https://wiki.xiph.org/Ogg'
license=(Xiph.org)
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/libogg-$pkgver.tar.gz")
sha256sums=('c2e8a485110b97550f453226ec644ebac6cb29d1caef2902c007edab4308d985')
makedepends=('devkitpro-pkgbuild-helpers')

build() {
  cd libogg-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd libogg-$pkgver

  make DESTDIR="$pkgdir" install
}

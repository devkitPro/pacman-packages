
# Maintainer: fincs <fincs.alt1@gmail.com>
pkgname=3ds-bulletphysics
pkgver=2.87
pkgrel=1
pkgdesc='Bullet Physics SDK: real-time collision detection and multi-physics simulation for VR, games, visual effects, robotics, machine learning etc. (for Nintendo 3DS homebrew development)'
arch=('any')
url='http://bulletphysics.org/'
license=('apache')
options=(!strip libtool staticlibs)
source=( "https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz" )
makedepends=('devkitpro-pkgbuild-helpers')
sha256sums=('438c151c48840fe3f902ec260d9496f8beb26dba4b17769a4a53212903935f95')

build() {
  cd bullet3-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/devkitarm.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fno-exceptions -fno-rtti" \
    -DCMAKE_AR="$DEVKITARM/bin/arm-none-eabi-gcc-ar" \
    -DBUILD_CPU_DEMOS:BOOL=OFF -DBUILD_BULLET3:BOOL=OFF -DBUILD_OPENGL3_DEMOS:BOOL=OFF -DBUILD_BULLET2_DEMOS:BOOL=OFF -DBUILD_EXTRAS:BOOL=OFF -DBUILD_UNIT_TESTS:BOOL=OFF \
    .

  make
}

package() {
  cd bullet3-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make install DESTDIR="$pkgdir"

  install -Dm644 "LICENSE.txt" "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.txt"
}

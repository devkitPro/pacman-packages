# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>

pkgname=3ds-jansson
pkgver=2.13
pkgrel=1
pkgdesc='A C library for encoding, decoding and manipulating JSON data.'
arch=('any')
url='http://www.digip.org/jansson/'
license=('custom')
options=(!strip libtool staticlibs)
makedepends=('devkitpro-pkgbuild-helpers')
source=("http://www.digip.org/jansson/releases/jansson-$pkgver.tar.gz")
groups=('3ds-portlibs')
sha256sums=('02c31bc16e702b30feb06d18bbfe086c0d8c938e906950980af7adcdb324541b')

build() {
  cd jansson-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd jansson-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libarchive
pkgver=3.1.2
pkgrel=2
pkgdesc='library that can create and read several streaming archive format (for Nintendo 3DS homebrew development)'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-3.1.2.patch"
)
sha256sums=(
 'eb87eacd8fe49e8d90c8fdc189813023ccc319c5e752b01fb6ad0cc7b2c53d5e'
 '3d397bbd8d374a516cdedcae4b8e7b33cb07f3324a3baca629bc3b6fe097a0f5'
)
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('3ds-bzip2' '3ds-zlib' '3ds-liblzma')
build() {
  cd libarchive-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  ./configure --disable-shared --enable-static --prefix=${PORTLIBS_PREFIX} --host=arm-none-eabi \
    --without-nettle --without-openssl --without-xml2 \
    --without-expat --without-iconv --disable-bsdtar \
    --disable-bsdcpio --disable-acl

  make
}

package() {
  cd libarchive-$pkgver

  source ${DEVKITPRO}/devkitarm.sh
  source ${DEVKITPRO}/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/bin
}


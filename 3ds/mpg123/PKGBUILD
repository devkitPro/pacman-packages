# Maintainer: Gabriel Kind <gabriel-dkp mastergk de>

pkgname=3ds-mpg123
pkgver=1.31.3
pkgrel=2
pkgdesc="A real time MPEG audio decoder for layer 1, 2 and 3"
arch=('any')
url="http://sourceforge.net/projects/mpg123"
license=('LGPL2.1')
options=(!strip libtool staticlibs)
groups=("3ds-portlibs")
source=(
 "https://downloads.sourceforge.net/sourceforge/mpg123/mpg123-${pkgver}.tar.bz2"
 "mpg123-${pkgver}.patch"
)
sha256sums=('1ca77d3a69a5ff845b7a0536f783fee554e1041139a6b978f6afe14f5814ad1a'
            '9af6c4c39d4cc58c5b81bf6e26a95fe9bf8322f583a98ec73f0362214a3c3b89'
)
makedepends=('dkp-toolchain-vars' '3ds-pkg-config')

build() {
  cd mpg123-$pkgver

  source /opt/devkitpro/3dsvars.sh

  patch -Np1 -i "$srcdir"/mpg123-${pkgver}.patch

  autoreconf -fi

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static --enable-fifo=no \
    --enable-ipv6=no --enable-network=no --enable-int-quality=no \
    --with-cpu=generic --with-default-audio=dummy

  make
}

package() {


  source /opt/devkitpro/3dsvars.sh

  cd mpg123-$pkgver

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -rf "$pkgdir"${PORTLIBS_PREFIX}/bin "$pkgdir"${PORTLIBS_PREFIX}/share
}

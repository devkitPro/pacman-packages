# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-sdl
pkgver=1.2.15
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Gamecube/Wii port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('ppc-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("${url}/release/SDL-${pkgver}.tar.gz" "SDL-1.2.15.patch")
groups=('ppc-portlibs' 'ppc-sdl-libs')
sha256sums=(
  'd6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00'
  '84c378ac90263c2e4ee24b8f9f62b2215b0f4a0233d61f0cbe5e554867dd53e9'
)

build() {
  cd SDL-$pkgver

  source /opt/devkitpro/ppcvars.sh

  patch -p1 -i $srcdir/SDL-1.2.15.patch
  ./autogen.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-nintendo-wii
  make
}

package() {
  cd SDL-$pkgver

  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

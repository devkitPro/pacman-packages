# Maintainer:  Dave Murphy <davem@devkitpro.org>

_pkgname=flac
pkgname=ppc-${_pkgname}
pkgver=1.5.0
pkgrel=1
pkgdesc='Free Lossless Audio Codec (FLAC) library.'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
groups=('ppc-portlibs')
depends=(
  'ppc-libogg'
)
makedepends=(
  'dkp-toolchain-vars'
)
source=("https://github.com/xiph/flac/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.xz")

build() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --host=powerpc-eabi \
      --disable-shared \
      --enable-static \
      --without-pic \
      --disable-doxygen-docs \
      --disable-examples \
      --disable-oggtest \
      --disable-programs \
      --disable-thorough-tests \
      --enable-cpplibs

  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  # copy license for libFLAC/libFLAC++
  install -Dm 644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" COPYING.Xiph

  # remove extra files
  rm -rfv "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('f2c1c76592a82ffff8413ba3c4a1299b6c7ab06c734dee03fd88630485c2b920')

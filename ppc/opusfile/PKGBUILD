# Maintainer:  Dave Murphy <davem@devkitpro.org>

_pkgname=opusfile
pkgname=ppc-${_pkgname}
pkgver=0.12
pkgrel=2
pkgdesc='Library for opening, seeking, and decoding .opus files'
arch=('any')
url='https://opus-codec.org/'
license=(BSD-3-Clause)
options=(!buildflags !strip libtool staticlibs)
groups=('ppc-portlibs')
depends=(
  'ppc-libogg'
  'ppc-libopus'
)
makedepends=(
  'dkp-toolchain-vars'
  'ppc-pkg-config'
)
source=("https://downloads.xiph.org/releases/opus/${_pkgname}-${pkgver}.tar.gz")

build() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --host=powerpc-eabi \
      --disable-shared \
      --enable-static \
      --enable-fixed-point  \
      --disable-examples \
      --disable-doc \
      --disable-http

  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  # install license
  install -Dm 644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" COPYING

  # remove docs
  rm -rfv "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('118d8601c12dd6a44f52423e68ca9083cc9f2bfe72da7a8c1acb22a80ae3550b')

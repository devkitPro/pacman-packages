# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=ppc-zlib
pkgver=1.2.11
pkgrel=3
pkgdesc='Deflate compression method library (for Nintendo gamecube/wii homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/packages/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')
makedepends=('devkitpro-pkgbuild-helpers')
groups=('ppc-portlibs')
build() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  CHOST=powerpc-eabi \
    ./configure --prefix="${PORTLIBS_PREFIX}" --static
  make libz.a # only build library, no programs

  # libminizip
  cd contrib/minizip
  autoreconf --force --verbose --install
  CFLAGS="${CFLAGS} -DUSE_FILE32API"
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  tail -n 31 README > "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share

  # libminizip
  cd contrib/minizip
  make DESTDIR="$pkgdir" install
}

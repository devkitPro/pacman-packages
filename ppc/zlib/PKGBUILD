# Maintainer: WinterMute <davem@devkitpro.org>

_libname=zlib
pkgname=ppc-${_libname}
pkgver=1.3.1
pkgrel=1
pkgdesc='The zlib compresion library.'
arch=('any')
url='http://www.zlib.net'
license=('zlib')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')
source=("${url}/${_libname}-${pkgver}.tar.gz")

build() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  CHOST=powerpc-eabi \
    ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --static

  make libz.a # only build library, no programs
}

package() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  # license
  install -Dm644 LICENSE "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"

  # remove useless stuff
  rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('9a93b2b7dfdac77ceba5a558a580e74667dd6fede4585b91eefb60f03b72df23')

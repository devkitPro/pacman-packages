# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=ppc-zlib
pkgver=1.2.11
pkgrel=2
pkgdesc='Deflate compression method library (for Nintendo gamecube/wii homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/packages/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')
build() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  CHOST=powerpc-eabi \
    ./configure --prefix="/opt/devkitpro/portlibs/ppc" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  source /opt/devkitpro/devkitppc.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"/opt/devkitpro/portlibs/ppc/licenses/$pkgname
  tail -n 31 README > "$pkgdir"/opt/devkitpro/portlibs/ppc/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/ppc/share
}

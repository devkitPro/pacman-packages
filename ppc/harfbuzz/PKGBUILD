# Maintainer:  Mary Strodl <ipadlover8322@gmail.com>

_pkgname=harfbuzz
pkgname=ppc-${_pkgname}
pkgver=12.3.2
pkgrel=1
pkgdesc='HarfBuzz text shaping engine library.'
arch=('any')
url='https://harfbuzz.github.io/'
license=(LGPL)
groups=('ppc-portlibs')
makedepends=(
  'dkp-meson-scripts'
  'ppc-pkg-config'
)
depends=('ppc-freetype')
options=(!buildflags !strip staticlibs)
source=("https://github.com/harfbuzz/harfbuzz/releases/download/${pkgver}/${_pkgname}-${pkgver}.tar.xz")

build() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  ${DEVKITPRO}/meson-cross.sh \
      ppc \
      ../crossfile.txt \
      build \
      -Dutilities=disabled \
      -Dtests=disabled \
      -Ddocs=disabled

  meson compile -C build -j 1
}

package() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  meson install -C build --destdir "$pkgdir"

  # license
  install -Dm 644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" COPYING
}

sha256sums=('6f6db164359a2da5a84ef826615b448b33e6306067ad829d85d5b0bf936f1bb8')

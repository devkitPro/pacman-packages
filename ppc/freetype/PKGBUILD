# Maintainer:  Dave Murphy <davem@devkitpro.org>

_pkgname=freetype
pkgname=ppc-${_pkgname}
pkgver=2.14.1
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL-2.0-only FTL)
options=(!strip libtool staticlibs)
groups=('ppc-portlibs')
depends=(
  'ppc-bzip2'
  'ppc-libpng'
  'ppc-zlib'
)
makedepends=(
  'dkp-toolchain-vars'
  'ppc-pkg-config'
)
source=("http://download.savannah.gnu.org/releases/freetype/${_pkgname}-${pkgver}.tar.xz")

build() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  # Note: freetype and harfbuzz have circular dependencies.
  # Build order:
  #   1. freetype with no harfbuzz
  #   2. harfbuzz
  #   3. freetype with harfbuzz
  USE_HARFBUZZ=yes

  ./configure \
      --host=powerpc-eabi \
      --prefix="$PORTLIBS_PREFIX" \
      --disable-shared \
       --enable-static \
       --disable-pic \
       --disable-mmap \
       --enable-year2038 \
       --with-zlib=yes \
       --with-bzip2=yes \
       --with-png=yes \
       --with-harfbuzz=$USE_HARFBUZZ \
       --with-brotli=yes

  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  # Licenses
  install -Dm 644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" LICENSE.TXT docs/GPLv2.TXT docs/FTL.TXT

  # Remove extra files
  rm -rfv "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('SKIP')

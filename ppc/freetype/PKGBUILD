# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-freetype
pkgver=2.9.1
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
depends=("ppc-bzip2"
         "ppc-libpng"
         "ppc-zlib"
)
makedepends=('ppc-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz")
sha256sums=('f57c1297f5ad2ad4764f491317fa0f548bd307c4513185d4a0602412e83b1dc9')

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --with-png

  make
}

package() {
  cd freetype-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${PORTLIBS_PREFIX}/bin/freetype-config"

  # Licenses
  install -Dm644 "docs/LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"

  # remove useless stuff
  rm -r "${pkgdir}/${PORTLIBS_PREFIX}/share"
}

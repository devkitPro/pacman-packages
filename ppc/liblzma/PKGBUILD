
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=ppc-liblzma
pkgver=5.8.2
pkgrel=1
pkgdesc='A compression library with an API similar to that of zlib.'
arch=('any')
url='https://tukaani.org'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://tukaani.org/xz/xz-${pkgver}.tar.xz")

makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
groups=('ppc-portlibs')

build() {

  cd xz-$pkgver

  source /opt/devkitpro/ppcvars.sh

   ./configure --prefix=${PORTLIBS_PREFIX} --host=powerpc-eabi \
      --enable-static --disable-shared --disable-threads \
      --disable-lzma-links --disable-xz --disable-xzdec \
      --disable-lzmadec --disable-lzmainfo --disable-nls \
      --disable-scripts --disable-doc

  make
}

package() {

  cd xz-$pkgver

  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install
  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/bin

}

sha256sums=('890966ec3f5d5cc151077879e157c0593500a522f413ac50ba26d22a9a145214')

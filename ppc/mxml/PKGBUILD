# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=ppc-mxml
pkgver=3.3.1
pkgrel=1
pkgdesc='XML parsing library'
arch=('any')
url='https://github.com/michaelrsweet/mxml'
license=('custom')
options=(!strip libtool staticlibs)
source=("${url}/releases/download/v${pkgver}/mxml-${pkgver}.tar.gz")
sha256sums=('0c663ed1fe393b5619f80101798202eea43534abd7c8aff389022fd8c1dacc32')
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
groups=('ppc-portlibs')

build() {

  cd mxml-${pkgver}

  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --disable-threads
  make
}

package() {

  source /opt/devkitpro/ppcvars.sh

  cd mxml-${pkgver}

  make BUILDROOT="$pkgdir" install

  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

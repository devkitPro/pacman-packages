# Maintainer: WinterMute <davem@devkitpro.org>
_libname=libjpeg-turbo
pkgname=ppc-${_libname}
pkgver=3.1.3
pkgrel=1
pkgdesc='libjpeg derivative with accelerated baseline JPEG compression and decompression'
arch=('any')
url='https://github.com/libjpeg-turbo/libjpeg-turbo'
license=('IJG')
options=(!strip libtool staticlibs)
source=("${_libname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz")
makedepends=('devkitppc-cmake' 'ppc-pkg-config' 'dkp-toolchain-vars')
groups=('ppc-portlibs')

build() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  powerpc-eabi-cmake \
      -S . \
      -B build \
      -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" \
      -DENABLE_SHARED=OFF \
      -DWITH_SIMD=OFF \
      -DWITH_TESTS=OFF \
      -DWITH_TOOLS=OFF

  make -C build
}

package() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make -C build install DESTDIR="$pkgdir"

  # license
  install -d "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}"
  cp -v LICENSE.md "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/"

  # remove useless stuff
  rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('3a13a5ba767dc8264bc40b185e41368a80d5d5f945944d1dbaa4b2fb0099f4e5')

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>

pkgname=ppc-jansson
pkgver=2.11
pkgrel=1
pkgdesc='A C library for encoding, decoding and manipulating JSON data.'
arch=('any')
url='http://www.digip.org/jansson/'
license=('custom')
options=(!strip libtool staticlibs)
makedepends=('devkitpro-pkgbuild-helpers')
source=("http://www.digip.org/jansson/releases/jansson-$pkgver.tar.gz")
sha256sums=('6e85f42dabe49a7831dbdd6d30dca8a966956b51a9a50ed534b82afc3fa5b2f4')

build() {
  cd jansson-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd jansson-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
}


# Maintainer: davidgfnet <david@davidgf.net>
pkgname=ppc-ode
pkgver=0.16.1
pkgrel=1
pkgdesc='High performance library for simulating rigid body dynamics'
arch=('any')
url='http://ode.org/'
license=('BSD')
options=(!strip libtool staticlibs)
source=("https://bitbucket.org/odedevs/ode/downloads/ode-${pkgver}.tar.gz")
makedepends=('devkitpro-pkgbuild-helpers')
sha256sums=('b228acad81f33781d53eaf313437cc5d6f66aec5a4e56c515fc1b2d51e6e8eba')
groups=('ppc-portlibs')

build() {
  cd ode-$pkgver

  source /opt/devkitpro/ppcvars.sh

  # Demos depend on GL and X11, disable since they are not useful to us.
  # Disable threads since there's no pthread support on Wii/GC
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
              --disable-demos --enable-static --disable-threading-intf

}

package() {

  cd ode-$pkgver

  source /opt/devkitpro/ppcvars.sh

  make install DESTDIR="$pkgdir"

}



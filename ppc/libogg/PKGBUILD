# Maintainer:  Dave Murphy <davem@devkitpro.org>

_pkgname=libogg
pkgname=ppc-${_pkgname}
pkgver=1.3.6
pkgrel=1
pkgdesc='Library to parse Ogg files.'
arch=('any')
url='https://wiki.xiph.org/Ogg'
license=(Xiph.org)
options=(!buildflags !strip libtool staticlibs)
groups=('ppc-portlibs')
makedepends=(
  'dkp-toolchain-vars'
  'ppc-pkg-config'
)
source=("https://downloads.xiph.org/releases/ogg/${_pkgname}-${pkgver}.tar.gz")

build() {
  cd "${_pkgname}-$pkgver"

  source "${DEVKITPRO}/ppcvars.sh"

  ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --host=powerpc-eabi \
      --disable-shared \
      --enable-static

  make
}

package() {
  cd "${_pkgname}-$pkgver"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  install -Dm644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" COPYING

  # remove useless documentation
  rm -r "${pkgdir}${PORTLIBS_PREFIX}/share/doc"
}

sha256sums=('83e6704730683d004d20e21b8f7f55dcb3383cdf84c0daedf30bde175f774638')

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

_libname=libpng
pkgname=ppc-${_libname}
pkgver=1.6.55
pkgrel=1
pkgdesc='PNG format graphic files library (for Nintendo Gamecube/Wii homebrew development)'
arch=('any')
url='http://www.libpng.org'
license=('libpng-2.0')
options=(!strip libtool staticlibs)
depends=('ppc-zlib')
makedepends=(
  'dkp-toolchain-vars'
  'ppc-pkg-config'
)
groups=('ppc-portlibs')
source=("https://download.sourceforge.net/libpng/${_libname}-${pkgver}.tar.gz")

build() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  ./configure \
    --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi \
    --disable-tools \
    --disable-tests \
    --disable-shared \
    --enable-static

  make
}

package() {
  cd "${_libname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install LN_S=cp DESTDIR="$pkgdir"

  # license
  install -Dm644 LICENSE "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE"

  # remove useless stuff
  rm -r "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('4b0abab6d219e95690ebe4db7fc9aa95f4006c83baaa022373c0c8442271283d')

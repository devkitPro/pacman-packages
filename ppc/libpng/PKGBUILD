# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=ppc-libpng
pkgver=1.6.39
pkgrel=2
pkgdesc='PNG format graphic files library (for Nintendo Gamecube/Wii homebrew development)'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('ppc-zlib')
makedepends=('ppc-pkg-config' 'dkp-toolchain-vars')
groups=('ppc-portlibs')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=(
  '1f4696ce70b4ee5f85f1e1623dc1229b210029fa4b7aee573df3e2ba7b036937'
)

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  source /opt/devkitpro/devkitppc.sh
  source /opt/devkitpro/ppcvars.sh

  make LN_S=cp DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

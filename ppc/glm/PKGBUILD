# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: Ash Logan <ash [at] heyquark [dot] com>

pkgname=ppc-glm
pkgver=0.9.9.2
pkgrel=2
pkgdesc='OpenGL Mathematics (GLM) header only C++ library'
arch=('any')
url='https://github.com/g-truc/glm'
license=(MIT)
options=(!strip libtool staticlibs)
source=("${url}/releases/download/${pkgver}/glm-${pkgver}.zip")
sha256sums=('209b5943d393925e1a6ecb6734e7507b8f6add25e72a605b25d0d0d382e64fd4')
makedepends=('dkp-toolchain-vars')
groups=('ppc-portlibs')

build() {

  mkdir -p build
  cd build

  source $DEVKITPRO/ppcvars.sh
  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/devkitppc.cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="$CFLAGS $CPPFLAGS" \
    -DGLM_TEST_ENABLE=OFF \
    ../glm

  make

}

package() {
  cd build

  make install DESTDIR="$pkgdir"
}

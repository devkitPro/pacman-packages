# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: Ash Logan <ash [at] heyquark [dot] com>
pkgname=ppc-glm
pkgver=1.0.2
pkgrel=1
pkgdesc='OpenGL Mathematics (GLM) header only C++ library'
arch=('any')
url='https://github.com/g-truc/glm'
license=(MIT)
options=(!strip libtool staticlibs)
source=("${url}/releases/download/${pkgver}/glm-${pkgver}.zip"
        "glm-${pkgver}-ppc.patch")
makedepends=('dkp-toolchain-vars' 'devkitppc-cmake')
groups=('ppc-portlibs')

build() {

  patch -Np1 -i ${srcdir}/glm-${pkgver}-ppc.patch -d ${srcdir}/glm

  source $DEVKITPRO/ppcvars.sh

  powerpc-eabi-cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="$CFLAGS $CPPFLAGS" \
    -DGLM_ENABLE_CXX_20=ON \
    -DGLM_ENABLE_LANG_EXTENSIONS=ON \
    -DGLM_BUILD_TESTS=OFF \
    -S ${srcdir}/glm -B _build

  cmake --build _build

}

package() {

  DESTDIR="$pkgdir" cmake --install _build
}

sha256sums=('203b64abc82e4d8697cc5aeedc93117fc1ee52d36e7cdcfc0854fa2c607edaae'
            'e07e962989b43c93df568941a8b7410465e5b232b5d6aeaa503b74f0cb01ddfe')

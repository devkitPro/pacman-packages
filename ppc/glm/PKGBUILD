# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: Ash Logan <ash [at] heyquark [dot] com>

pkgname=ppc-glm
pkgver=0.9.9.3
pkgrel=1
pkgdesc='OpenGL Mathematics (GLM) header only C++ library'
arch=('any')
url='https://github.com/g-truc/glm'
license=(MIT)
options=(!strip libtool staticlibs)
source=("${url}/releases/download/${pkgver}/glm-${pkgver}.zip")
sha256sums=('496e855590b8aa138347429b7fc745d66707303fb82c1545260d1888472e137b')
makedepends=('devkitpro-pkgbuild-helpers')

build() {

  mkdir -p build
  cd build

  source $DEVKITPRO/ppcvars.sh

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/devkitppc.cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SYSTEM_NAME=Generic \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGLM_TEST_ENABLE=OFF \
    ../glm

  make

}

package() {
  cd build

  make install DESTDIR="$pkgdir"
}

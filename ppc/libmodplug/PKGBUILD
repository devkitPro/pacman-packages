# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=ppc-libmodplug
pkgver=0.8.9.0
pkgrel=2
pkgdesc='Play various .mod formats'
arch=('any')
url='http://modplug-xmms.sourceforge.net/'
license=(public domain)
options=(!strip libtool staticlibs)
depends=("ppc-zlib")
source=(
  "https://sourceforge.net/projects/modplug-xmms/files/libmodplug/$pkgver/libmodplug-$pkgver.tar.gz"
)
sha256sums=(
  '457ca5a6c179656d66c01505c0d95fafaead4329b9dbaa0f997d00a3508ad9de'
)
groups=("ppc-portlibs")

build() {
  cd libmodplug-$pkgver

  source /opt/devkitpro/ppcvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static

  make
}

package() {
  cd libmodplug-$pkgver

  make DESTDIR="$pkgdir" install
}

# Maintainer: Gabriel Kind <gabriel-dkp mastergk de>

_pkgname=mpg123
pkgname=ppc-${_pkgname}
pkgver=1.33.4
pkgrel=1
pkgdesc="A real time MPEG audio decoder for layer 1, 2 and 3"
arch=('any')
url="http://sourceforge.net/projects/mpg123"
license=('LGPL-2.1-only')
options=(!buildflags !strip libtool staticlibs)
makedepends=(
  'devkitPPC'
  'dkp-toolchain-vars'
  'ppc-pkg-config'
)
groups=('ppc-portlibs')
source=("http://downloads.sourceforge.net/sourceforge/mpg123/${_pkgname}-${pkgver}.tar.bz2")

build() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  ./configure \
      --prefix="${PORTLIBS_PREFIX}" \
      --host=powerpc-eabi \
      --disable-shared \
      --enable-static \
      --disable-components \
      --enable-libmpg123 \
      --enable-libsyn123 \
      --disable-fifo \
      --disable-int-quality \
      --disable-ipv6 \
      --disable-network \
      --with-cpu=generic
 
  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  source "${DEVKITPRO}/ppcvars.sh"

  make install DESTDIR="$pkgdir"

  # install license
  install -Dm 644 -t "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}" COPYING
  
  # remove empty bin dir
  rm -rfv "${pkgdir}${PORTLIBS_PREFIX}/bin"
}

sha256sums=('3ae8c9ff80a97bfc0e22e89fbcd74687eca4fc1db315b12607f27f01cb5a47d9')

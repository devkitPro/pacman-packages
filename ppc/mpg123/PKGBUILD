# Maintainer: Gabriel Kind <gabriel-dkp mastergk de>

pkgname=ppc-mpg123
pkgver=1.31.3
pkgrel=2
pkgdesc="A real time MPEG audio decoder for layer 1, 2 and 3"
arch=('any')
url="http://sourceforge.net/projects/mpg123"
license=('LGPL2.1')
options=(!strip libtool staticlibs)
source=(
 "http://downloads.sourceforge.net/sourceforge/mpg123/mpg123-${pkgver}.tar.bz2"
 "mpg123-${pkgver}.patch"
)
sha256sums=('1ca77d3a69a5ff845b7a0536f783fee554e1041139a6b978f6afe14f5814ad1a'
            'e23f7c9981f26c1748b3e4cb72eed4a1c6fd92f7c51f6d6cc97e2ad2001f0cd3')

makedepends=('dkp-toolchain-vars' 'ppc-pkg-config' 'devkitPPC')
groups=('ppc-portlibs')

build() {
  cd mpg123-$pkgver

  source /opt/devkitpro/ppcvars.sh

  patch -Np1 -i "$srcdir"/mpg123-${pkgver}.patch

  autoreconf -fi

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=powerpc-eabi \
    --disable-shared --enable-static --enable-fifo=no \
    --enable-ipv6=no --enable-network=no --enable-int-quality=no \
    --with-cpu=generic --with-default-audio=dummy

  make
}

package() {
  cd mpg123-$pkgver

  source /opt/devkitpro/devkitppc.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -rf "$pkgdir"${PORTLIBS_PREFIX}/bin "$pkgdir"${PORTLIBS_PREFIX}/share
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: rsn8887 <raist66676@gmx.de>

pkgname=switch-liblzma
pkgver=5.8.2
pkgrel=1
pkgdesc='A compression library with an API similar to that of zlib'
arch=('any')
url='https://tukaani.org/xz/'
license=('public domain')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
groups=('switch-portlibs')
source=("https://tukaani.org/xz/xz-${pkgver}.tar.gz")

build() {
  cd xz-${pkgver}

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  cd src/liblzma
  make
}

package() {
  cd xz-${pkgver}

  source /opt/devkitpro/switchvars.sh

  cd src/liblzma
  make DESTDIR="${pkgdir}" install
}

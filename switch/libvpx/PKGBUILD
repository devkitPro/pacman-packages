# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: uyjulian <uyjulian@gmail.com>

pkgname=switch-libvpx
pkgver=1.8.0
pkgrel=1
pkgdesc='libvpx is a free software video codec library from Google and the Alliance for Open Media (AOMedia). It serves as the reference software implementation for the VP8 and VP9 video coding formats.'
arch=('any')
url='https://chromium.googlesource.com/webm/libvpx'
license=('BSD')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
source=("https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz" "libvpx.patch")
sha256sums=(
  '86df18c694e1c06cc8f83d2d816e9270747a0ce6abe316e93a4f4095689373f6'
  'b51f4661334646beda13d8fc9dd8fcc27eb91813ae9cd76ab4ac6914efb0c9da'
)

build() {
  cd libvpx-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -p1 -i $srcdir/libvpx.patch

  CROSS=aarch64-none-elf- ./configure \
    --prefix="${PORTLIBS_PREFIX}" --target=generic-gnu \
    --disable-examples --disable-tools \
    --disable-docs --disable-unit-tests \
    --disable-shared --enable-static 

  make
}

package() {
  cd libvpx-$pkgver

  make DESTDIR="$pkgdir" install
}


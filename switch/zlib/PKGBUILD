
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Deflate compression method library (for Nintendo Switch homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/portlibs/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')

build() {
  cd zlib-$pkgver
  CFLAGS="-g -O2 -march=armv8-a -mtune=cortex-a57 -mtp=soft -fPIC -ftls-model=local-exec" \
  CHOST=aarch64-none-elf \
    ./configure --prefix="/opt/devkitpro/portlibs/switch" --static
  make libz.a # only build library, no programs
}

package() {
  cd zlib-$pkgver

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname
  tail -n 31 README > "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/switch/share
}

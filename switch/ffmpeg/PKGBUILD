# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: Ezekiel Bethel <stary@9net.org>
# Contributor: carstene1ns <dev f4ke de>
# Contributor: jakibaki <jakibaki live com>

pkgname=switch-ffmpeg
pkgver=4.1.1
pkgrel=2
pkgdesc='ffmpeg port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://ffmpeg.org/'
license=('LGPL' 'GPL')
options=(!strip staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('switch-zlib' 'switch-bzip2' 'switch-libass' 'switch-libfribidi'
         'switch-freetype' 'switch-libopus' 'switch-libtheora' 'switch-libvpx')
source=("https://ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz" "ffmpeg.patch")
sha256sums=(
 '373749824dfd334d84e55dff406729edfd1606575ee44dd485d97d45ea4d2d86'
 'a1cd32e822c9b027b061ffc8cd2f85c429554f2f7a974caff12518a19545700b'
)
groups=('switch-portlibs')

prepare() {
  cd ffmpeg-$pkgver
  patch -Np1 -i "$srcdir/ffmpeg.patch"
}

build() {
  cd ffmpeg-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix=$PORTLIBS_PREFIX --disable-shared --enable-static \
    --cross-prefix=aarch64-none-elf- --enable-cross-compile \
    --arch=aarch64 --target-os=horizon --enable-pic --disable-asm \
    --extra-cflags='-D__SWITCH__ -D_GNU_SOURCE -O2 -march=armv8-a -mtune=cortex-a57 -mtp=soft -fPIC -ftls-model=local-exec' \
    --extra-cxxflags='-D__SWITCH__ -D_GNU_SOURCE -O2 -march=armv8-a -mtune=cortex-a57 -mtp=soft -fPIC -ftls-model=local-exec' \
    --extra-ldflags='-fPIE -L${PORTLIBS_PREFIX}/lib -L${DEVKITPRO}/libnx/lib' \
    --disable-runtime-cpudetect --disable-programs --disable-debug --disable-doc \
    --enable-network --disable-hwaccels --disable-encoders \
    --disable-avdevice --enable-swscale --enable-swresample \
    --enable-libass --enable-libfreetype --enable-libfribidi --enable-libopus --enable-libtheora --enable-libvpx \
    --enable-filter='rotate,transpose' \
    --disable-protocols --enable-protocol=file,http,ftp,tcp,udp,rtmp \
    --disable-demuxers --enable-demuxer=h264,matroska,mov,ogg,rtsp,mpegts \
    --disable-parsers --enable-parser=h264,aac

  make
}

package() {
  cd ffmpeg-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove examples
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

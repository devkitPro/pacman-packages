# Maintainer: Ezekiel Bethel <zek@9net.org>
pkgname=switch-physfs
pkgver=3.0.2
pkgrel=1
pkgdesc='PhysicsFS is a library to provide abstract access to various archives. (for Nintendo Switch homebrew development)'
arch=('any')
url='https://icculus.org/physfs/'
license=('zlib')
options=(!strip libtool staticlibs)
source=( "https://icculus.org/physfs/downloads/physfs-${pkgver}.tar.bz2"
         "physfs-${pkgver}.patch")
makedepends=('devkitA64' 'dkp-toolchain-vars')
sha256sums=(
  '304df76206d633df5360e738b138c94e82ccf086e50ba84f456d3f8432f9f863'
  '9e6b0387a36364a784ab0dd4673d7f81e59da47dd2c5ca02bd6a546c63fe02e3'
  )
groups=('switch-portlibs')
build() {
  cd physfs-$pkgver

  patch -Np1 -i "$srcdir"/physfs-${pkgver}.patch

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/switch.cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/switch \
    -DPHYSFS_BUILD_STATIC=ON -DPHYSFS_BUILD_SHARED=OFF \
    -DPHYSFS_BUILD_TEST=OFF \
    .

  make
}

package() {
  cd physfs-$pkgver

  make install DESTDIR="$pkgdir"
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: uyjulian <uyjulian@gmail.com>

pkgname=switch-libwebp
pkgver=1.0.2
pkgrel=1
pkgdesc='WebP is a modern image format that provides superior lossless and lossy compression for images on the web. Using WebP, webmasters and web developers can create smaller, richer images that make the web faster.'
arch=('any')
url='https://developers.google.com/speed/webp/'
license=('BSD')
options=(!strip libtool staticlibs)
makedepends=('devkitpro-pkgbuild-helpers')
source=("https://storage.googleapis.com/downloads.webmproject.org/releases/webp/libwebp-$pkgver.tar.gz")
sha256sums=('3d47b48c40ed6476e8047b2ddb81d93835e0ca1b8d3e8c679afbb3004dd564b1')

build() {
  cd libwebp-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static 

  make
}

package() {
  cd libwebp-$pkgver

  make DESTDIR="$pkgdir" install
}


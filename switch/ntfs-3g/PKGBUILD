# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>
# Maintainer: DarkMatterCore <pabloacurielz@gmail.com>

pkgname=switch-ntfs-3g
pkgver=2022.10.3
pkgrel=2
pkgdesc='An open source, freely available read/write NTFS driver'
arch=('any')
url='https://www.tuxera.com/community/open-source-ntfs-3g/'
license=('GPL')
options=(!strip libtool staticlibs)
groups=('switch-portlibs')
makedepends=('devkitA64' 'dkp-toolchain-vars')

source=(
  "ntfs-3g-${pkgver}.tar.gz::https://github.com/tuxera/ntfs-3g/archive/refs/tags/${pkgver}.tar.gz"
  "ntfs-3g-${pkgver}.patch"
)

prepare() {
  cd "ntfs-3g-${pkgver}"
  patch -Np1 -i "${srcdir}/ntfs-3g-${pkgver}.patch"
  ./autogen.sh
}

build() {
  cd "ntfs-3g-${pkgver}"

  source /opt/devkitpro/switchvars.sh

  CPPFLAGS=-I/opt/devkitpro/libnx/include ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf \
    --disable-shared \
    --enable-static \
    --disable-ntfsprogs \
    --disable-ntfs-3g \
    --disable-device-default-io-ops \
    --disable-plugins \
    --without-uuid \
    --without-hd

  make
}

package() {
  cd "ntfs-3g-${pkgver}"

  source /opt/devkitpro/switchvars.sh

  make install DESTDIR=${pkgdir}

  install -Dm644 COPYING "${pkgdir}${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING"

  rm -fr "${pkgdir}${PORTLIBS_PREFIX}/share"
}

sha256sums=('8bd7749ea9d8534c9f0664d48b576e90b96d45ec8803c9427f6ffaa2f0dde299'
            '484253654d8a0ed090b20a1b37deba54b1537dd688531c8e914e4494b5a2cd8f')

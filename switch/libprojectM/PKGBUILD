# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgname=switch-libprojectm
pkgver=r1866.4befa4516
pkgrel=1
pkgdesc='Cross-platform music visualization. Open-source and Milkdrop-compatible'
arch=('any')
url='https://github.com/projectM-visualizer/projectm'
license=('(L)GPL')
options=(!strip libtool staticlibs)
source=(${pkgname}::"git+https://github.com/projectM-visualizer/projectm.git#commit=4befa4516fedf1c591fbd4e39476ca67f89af6b2")
sha256sums=('SKIP')
makedepends=('git' 'switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')

pkgver() {
  cd "$srcdir/${pkgname}"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname}
  patch -Np1 -i "$srcdir/../projectM-4befa4516-disable_navtive_preset_factory.patch"
  patch -Np1 -i "$srcdir/../projectM-4befa4516-platform_definitions.patch"
}

build() {
  cd ${pkgname}

  source /opt/devkitpro/switchvars.sh

  libtoolize
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-threading --disable-sdl

  make
}

package() {
  cd ${pkgname}
  
  make DESTDIR="$pkgdir" install
  
  install -Dm644 LICENSE.txt "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/LICENSE
}

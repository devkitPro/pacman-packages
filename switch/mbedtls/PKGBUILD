pkgname=switch-mbedtls
pkgver=2.16.6
pkgrel=3
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
depends=(switch-zlib)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=( 
    "https://tls.mbed.org/download/mbedtls-${pkgver}-apache.tgz"
    'switch-mbedtls.patch'
)
groups=('switch-portlibs')
sha256sums=(
 '66455e23a6190a30142cdc1113f7418158839331a9d8e6b0778631d077281770'
 'c1cb20905a73e2cb9a6a97c73bce56576f06d7c63cbe1875c4d8c1c7f98faba1'
)

build() {
  cd mbedtls-$pkgver

  patch -Np1 -i $srcdir/switch-mbedtls.patch

  ./scripts/config.pl set MBEDTLS_ENTROPY_HARDWARE_ALT
  ./scripts/config.pl set MBEDTLS_NO_PLATFORM_ENTROPY

  ./scripts/config.pl unset MBEDTLS_SELF_TEST

  source /opt/devkitpro/switchvars.sh

  cmake -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/devkita64.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="CFLAGS -fno-exceptions -fno-rtti" \
    -DZLIB_ROOT="$PORTLIBS_PREFIX" \
    -DENABLE_ZLIB_SUPPORT=TRUE -DENABLE_TESTING=FALSE -DENABLE_PROGRAMS=FALSE \
    .

  make

}

package() {

  cd mbedtls-$pkgver

  source /opt/devkitpro/switchvars.sh

  make install DESTDIR="$pkgdir"

  install -d "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname
  cp -v LICENSE "$pkgdir"$PORTLIBS_PREFIX/licenses/$pkgname 

}

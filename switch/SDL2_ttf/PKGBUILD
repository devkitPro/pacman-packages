# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>
# Contributor: giroletm <rsmromaji@gmail.com>

pkgname=switch-sdl2_ttf
pkgver=2.20.2
pkgrel=1
pkgdesc="SDL2 TrueType font library (Nintendo Switch port)."
arch=('any')
url="https://libsdl.org/projects/SDL_ttf/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=(
  'switch-sdl2'
  'switch-freetype'
)
source=("${url}release/SDL2_ttf-${pkgver}.tar.gz" "SDL2_ttf-${pkgver}.patch")
sha256sums=('9dc71ed93487521b107a2c4a9ca6bf43fb62f6bddd5c26b055e6b91418a22053'
            '72c810610ae772692e4664b95cc64ec5aa3cae7ba4a7091d0830a10f038762e2')
groups=('switch-portlibs' 'switch-sdl2')

build() {
  cd SDL2_ttf-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  patch -p1 -i $srcdir/SDL2_ttf-${pkgver}.patch

  # patch out compiling showfont and glfont
  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf --disable-shared --enable-static \
    --disable-sdltest --without-x

  make
}

package() {
  cd SDL2_ttf-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # add our static lib
  echo "Requires.private: freetype2" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_ttf.pc"

  # License
  install -Dm644 "LICENSE.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.txt"
}

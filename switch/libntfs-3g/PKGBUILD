# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgname=switch-libntfs-3g
pkgver=2017.3.23
pkgrel=2
pkgdesc='Read-Write NTFS Driver'
arch=('any')
url='https://www.tuxera.com/community/open-source-ntfs-3g/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://tuxera.com/opensource/ntfs-3g_ntfsprogs-${pkgver}.tgz")
sha256sums=('3e5a021d7b761261836dcb305370af299793eedbded731df3d6943802e1262d5')
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')

prepare() {
  cd ntfs-3g_ntfsprogs-$pkgver
  patch -Np1 -i "$srcdir/../ntfs-3g_ntfsprogs-2017.3.23-fix_syslog_include.patch"
  patch -Np1 -i "$srcdir/../ntfs-3g_ntfsprogs-2017.3.23-timespec_already_defined.patch"
  patch -Np1 -i "$srcdir/../ntfs-3g_ntfsprogs-2017.3.23-define_dev_major_minor.patch"
  patch -Np1 -i "$srcdir/../ntfs-3g_ntfsprogs-2017.3.23-remove_install_exec_hook_target.patch"
}

build() {
  cd ntfs-3g_ntfsprogs-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-device-default-io-ops \
    --disable-ntfs-3g --disable-ntfsprogs --disable-plugins --disable-crypto \
    --without-uuid --without-hd

  make
}

package() {
  cd ntfs-3g_ntfsprogs-$pkgver

  make DESTDIR="$pkgdir" install
  
  install -Dm644 COPYING.LIB "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/COPYING
  rm -r "$pkgdir"/opt/devkitpro/portlibs/switch/share
}

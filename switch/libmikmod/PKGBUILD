# Maintainer: Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <dev f4ke de>

pkgname=switch-libmikmod
pkgver=3.3.11.1
pkgrel=1
pkgdesc='A portable sound library supporting many tracker formats (Nintendo Switch port)'
arch=('any')
url="http://mikmod.sourceforge.net/"
license=('LGPL')
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("https://sourceforge.net/projects/mikmod/files/libmikmod/$pkgver/libmikmod-$pkgver.tar.gz"
        "libmikmod-switch-support.patch")
sha256sums=('ad9d64dfc8f83684876419ea7cd4ff4a41d8bcd8c23ef37ecb3a200a16b46d19'
            '624350141cfcae38301622c7bc8df0d0d698282d7aca3033c5083c701e7bb20f')

groups=('switch-portlibs')

prepare() {
  cd libmikmod-$pkgver
  patch -Np1 < ../libmikmod-switch-support.patch

  autoreconf -i
}

build() {
  cd libmikmod-$pkgver

  source ${DEVKITPRO}/devkita64.sh
  source ${DEVKITPRO}/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static

  make
}

package() {
  cd libmikmod-$pkgver

  source ${DEVKITPRO}/devkita64.sh
  source ${DEVKITPRO}/switchvars.sh

  make DESTDIR="$pkgdir" install

  rm -fr "$pkgdir"${PORTLIBS_PREFIX}/share
}

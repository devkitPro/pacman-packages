# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=switch-libogg
pkgver=1.3.4
pkgrel=2
pkgdesc='The Ogg transport bitstream is designed to provide framing, error protection and seeking structure for higher-level codec streams that consist of raw, unencapsulated data packets, such as the Opus, Vorbis and FLAC audio codecs or the Theora and Dirac video codecs.'
arch=('any')
url='https://wiki.xiph.org/Ogg'
license=(Xiph.org)
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/libogg-$pkgver.tar.gz")
sha256sums=('fe5670640bd49e828d64d2879c31cb4dde9758681bb664f9bdbf159a01b0c76e')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
groups=('switch-portlibs')
build() {
  cd libogg-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static

  make
}

package() {
  cd libogg-$pkgver

  make DESTDIR="$pkgdir" install
}

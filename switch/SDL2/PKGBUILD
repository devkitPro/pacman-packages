# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-sdl2
pkgver=2.0.10
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Switch port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('switch-mesa' 'libnx')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('switch-portlibs' 'switch-sdl2')
sha256sums=(
  'b4656c13a1f0d0023ae2f4a9cf08ec92fffb464e0f24238337784159b8b91d57'
  '47b84f5c3ad743659ea45dcd47a942a292e1a1802fe3e004486b14f09a5954c2'
)

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -p1 -i $srcdir/SDL2-2.0.10.patch

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
  --disable-shared --enable-static \
  --enable-audio \
  --enable-joystick \
  --disable-power \
  --disable-cpuinfo \
  --disable-filesystem \
  --enable-threads \
  --enable-timers \
  --enable-video

  make
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

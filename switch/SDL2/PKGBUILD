# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=switch-sdl2
pkgver=2.0.8
pkgrel=5
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo Switch port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-2.0.8.patch")
sha256sums=(
'edc77c57308661d576e843344d8638e025a7818bff73f8fbfab09c3c5fd092ec'
'b91babb4a5714cd18b29be72284042f03982d61e899df9e6bcf9bf9ddb4002ad'
)

build() {
  cd SDL2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  patch -p1 -i $srcdir/SDL2-2.0.8.patch
  ./autogen.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
  --disable-shared --enable-static \
  --enable-audio \
  --enable-joystick \
  --disable-power \
  --disable-cpuinfo \
  --disable-filesystem \
  --enable-threads \
  --enable-timers \
  --enable-video

  make
}

package() {
  cd SDL2-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

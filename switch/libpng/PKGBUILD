# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=switch-libpng
pkgver=1.6.37
pkgrel=1
pkgdesc='PNG format graphic files library (for Nintendo Switch homebrew development)'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('switch-zlib')
groups=('switch-portlibs')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=('505e70834d35383537b6491e7ae8641f1a4bed1876dbfe361201fc80868d88ca')

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in

  ./configure --prefix="$DEVKITPRO/portlibs/switch" --host=aarch64-none-elf \
    --disable-shared --enable-static
}

package() {
  cd libpng-$pkgver

  make LN_S=cp DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"/opt/devkitpro/portlibs/switch/share
}

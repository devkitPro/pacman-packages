# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Kevoot <kholmesdev@gmail.com>

pkgname=switch-jansson
pkgver=2.11
pkgrel=3
pkgdesc='A C library for encoding, decoding and manipulating JSON data.'
arch=('any')
url='http://www.digip.org/jansson/'
license=('custom')
options=(!strip libtool staticlibs)
makedepends=('dkp-toolchain-vars')
source=("http://www.digip.org/jansson/releases/jansson-$pkgver.tar.gz")
sha256sums=('6e85f42dabe49a7831dbdd6d30dca8a966956b51a9a50ed534b82afc3fa5b2f4')
groups=('switch-portlibs')

build() {
  cd jansson-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static
  make
}

package() {
  cd jansson-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
}

# Maintainer:  fincs <fincs.alt1@gmail.com>

pkgbasename=glfw
pkgname=switch-$pkgbasename
pkgver=3.3.4
pkgrel=2
pkgdesc='Open Source, multi-platform library for OpenGL, OpenGL ES and Vulkan development on the desktop'
arch=('any')
url='https://github.com/glfw/glfw'
license=(MIT)
options=(!strip libtool staticlibs)
source=(
  "${url}/releases/download/${pkgver}/${pkgbasename}-${pkgver}.zip"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  'bbd2c42c660b725e9755eb417e40b373f0d4c03138c9b2e210d02cd308bd99cd'  # glfw-3.3.4.zip
  '3af95bc53a11472f024ad8518a2013b52ce3b93c73d66691da992279306663f9'  # switch-glfw-3.3.4.patch
)
depends=('switch-mesa')
makedepends=('dkp-toolchain-vars' 'switch-cmake')
groups=('switch-portlibs')

build() {

  cd ${pkgbasename}-${pkgver}
  patch -p1 -i ${srcdir}/${pkgname}-${pkgver}.patch

  source ${DEVKITPRO}/switchvars.sh

  aarch64-none-elf-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DGLFW_BUILD_EXAMPLES:BOOL=OFF -DGLFW_BUILD_TESTS:BOOL=OFF -DGLFW_BUILD_DOCS:BOOL=OFF \
    -DGLFW_VULKAN_STATIC:BOOL=ON -DGLFW_EGL_STATIC:BOOL=ON \
    .

  make

}

package() {
  cd ${pkgbasename}-${pkgver}

  make install DESTDIR="$pkgdir"
}

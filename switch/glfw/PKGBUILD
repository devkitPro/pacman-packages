# Maintainer:  fincs <fincs.alt1@gmail.com>
# Maintainer:  DarkMatterCore <pabloacurielz@gmail.com>

pkgbasename=glfw
pkgname=switch-$pkgbasename
pkgver=3.3.2
pkgrel=2
pkgdesc='Open Source, multi-platform library for OpenGL, OpenGL ES and Vulkan development on the desktop'
arch=('any')
url='https://github.com/glfw/glfw'
license=(MIT)
options=(!strip libtool staticlibs)
source=(
  "${url}/releases/download/${pkgver}/${pkgbasename}-${pkgver}.zip"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  '08a33a512f29d7dbf78eab39bd7858576adcc95228c9efe8e4bc5f0f3261efc7'
  'd808b37a9a9e37b6b8b99d8fc3f1e05d688d5ee3a9b7f5b208a3846915869a3a'
)
depends=('switch-mesa')
makedepends=('devkitpro-pkgbuild-helpers')

build() {

  cd ${pkgbasename}-${pkgver}
  patch -p1 -i ${srcdir}/${pkgname}-${pkgver}.patch

  source /opt/devkitpro/switchvars.sh

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE="${DEVKITPRO}/switch.cmake" \
    -DCMAKE_INSTALL_PREFIX="${PORTLIBS_PREFIX}" \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DCMAKE_AR="${DEVKITPRO}/devkitA64/bin/${AR}" \
    -DSWITCH_LIBNX:BOOL=ON \
    -DGLFW_BUILD_EXAMPLES:BOOL=OFF -DGLFW_BUILD_TESTS:BOOL=OFF -DGLFW_BUILD_DOCS:BOOL=OFF \
    -DGLFW_VULKAN_STATIC:BOOL=ON -DGLFW_EGL_STATIC:BOOL=ON \
    .

  make

}

package() {
  cd ${pkgbasename}-${pkgver}

  make install DESTDIR="$pkgdir"
}

# Maintainer: COMP Omega <compomega@tutanota.com>
pkgname=switch-mygui
pkgver=3.4.0
pkgrel=1
pkgdesc='Fast, flexible and simple GUI.'
arch=('any')
url='http://mygui.info'
license=('MIT')
options=(!strip libtool staticlibs)
source=( "https://github.com/MyGUI/mygui/archive/MyGUI${pkgver}.tar.gz")
makedepends=('devkitA64' 'devkitpro-pkgbuild-helpers')
depends=('switch-zlib' 'switch-zziplib' 'switch-freetype' 'switch-ogre')
sha256sums=(
  'd1d5f294670ae71f7200ed4b30859018281d8cfd45d6a38d18b97a4aba604c42')
groups=('switch-portlibs')
build() {
  cd mygui-MyGUI$pkgver

  source /opt/devkitpro/switchvars.sh

  cmake -G"Unix Makefiles" \
    -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/switch.cmake \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DMYGUI_STATIC=ON -DMYGUI_DISABLE_PLUGINS=ON \
    -DMYGUI_BUILD_DEMOS=OFF -DMYGUI_BUILD_PLUGINS=OFF \
    -DMYGUI_BUILD_TOOLS=OFF -DMYGUI_BUILD_UNITTESTS=OFF \
    -DMYGUI_BUILD_TEST_APP=OFF -DMYGUI_BUILD_WRAPPER=OFF \
    .

  make

}

package() {

  cd mygui-MyGUI$pkgver

  source /opt/devkitpro/switchvars.sh

  make install DESTDIR="$pkgdir"

}

# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Pablo Curiel <pabloacurielz@gmail.com>

pkgname=switch-libxml2
pkgver=2.9.8
pkgrel=4
pkgdesc='XML parsing library, version 2.'
arch=('any')
url='http://www.xmlsoft.org/'
license=('MIT')
options=(!strip libtool staticlibs)
source=(
  "http://xmlsoft.org/sources/libxml2-${pkgver}.tar.gz"
)
sha256sums=(
  '0b74e51595654f958148759cfef0993114ddccccbb6f31aee018f3558e8e2732'
)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
groups=('switch-portlibs')
build() {
  cd libxml2-$pkgver

  source /opt/devkitpro/switchvars.sh

  export LIBS=-lnx
  export CFLAGS="${CFLAGS} -D__LINUX_ERRNO_EXTENSIONS__"


  # patch out compiling xmlcatalog & xmllint
  sed '/^bin_PROGRAMS/d' -i Makefile.in

 ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-maintainer-mode \
    --enable-rebuild-docs=no --enable-ipv6=no \
    --without-icu --without-python --without-lzma

  make
}

package() {
  cd libxml2-$pkgver

  make DESTDIR="$pkgdir" install

  #remove uneeded extras
  rm -fr $pkgdir/doc $pkgdir/man $pkgdir/gtk-doc 


}

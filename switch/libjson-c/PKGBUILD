# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Pablo Curiel <pabloacurielz@gmail.com>

pkgbasename=json-c
pkgname=switch-lib${pkgbasename}
pkgver=0.15
pkgrel=1
pkgdesc='A JSON implementation in C.'
arch=('any')
url='https://github.com/json-c/json-c/'
license=('MIT')
options=(!strip libtool staticlibs)
source=("https://s3.amazonaws.com/json-c_releases/releases/json-c-${pkgver}.tar.gz")
sha256sums=('b8d80a1ddb718b3ba7492916237bbf86609e9709fb007e7f7d4322f02341a4c6')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars' 'switch-cmake')
groups=('switch-portlibs')

build() {
  cd "${pkgbasename}-${pkgver}"

  source ${DEVKITPRO}/switchvars.sh

  aarch64-none-elf-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX="$PORTLIBS_PREFIX" -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=OFF -DBUILD_STATIC_LIBS=ON \
    -DUNIX=1 .

  make
}

package() {
  cd "${pkgbasename}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

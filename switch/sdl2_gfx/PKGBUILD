# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=switch-sdl2_gfx
pkgver=1.0.4
pkgrel=1
pkgdesc='Graphics drawing primitives and other support functions.'
arch=('any')
url='http://www.ferzkopp.net/wordpress/2016/01/02/sdl_gfx-sdl2_gfx/'
license=(zlib)
options=(!strip libtool staticlibs)
depends=('switch-sdl2')
source=("http://www.ferzkopp.net/Software/SDL2_gfx/SDL2_gfx-$pkgver.tar.gz")
sha256sums=('63e0e01addedc9df2f85b93a248f06e8a04affa014a835c2ea34bfe34e576262')

build() {
  cd SDL2_gfx-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  # add aarch64 host
  sed -i '250i	| aarch64 | aarch64_be \\' config.sub
  sed -i '358i	| aarch64-* | aarch64_be-* \\' config.sub
  
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static \
    --disable-mmx

  make
}

package() {
  cd SDL2_gfx-$pkgver

  make DESTDIR="$pkgdir" install
}

# Maintainer: Rhys Koedijk <rhys@koedijk.co.nz>

pkgbasename=ftgl
pkgname=switch-$pkgbasename
pkgver=2.3
pkgrel=1
pkgdesc='FTGL is a free cross-platform Open Source C++ library that uses Freetype2 to simplify rendering fonts in OpenGL applications'
arch=('any')
url='https://github.com/ulrichard/ftgl'
license=('MIT')
options=(!strip libtool staticlibs)
source=(${pkgname}::"git+${url}.git#commit=16f7667cbd4b1e988ae24128c5dea54479926f85")
sha256sums=('SKIP')
depends=('switch-freetype' 'switch-glu' 'switch-mesa')
makedepends=('git' 'switch-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('switch-portlibs')

prepare() {
  cd ${pkgname}
  patch -Np1 -i "${startdir}/${pkgbasename}-16f7667-install-pkg-config.patch"
}

build() {
  cd ${pkgname}

  source /opt/devkitpro/switchvars.sh

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/switch.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CFLAGS" \
    -DCMAKE_AR="/opt/devkitpro/devkitA64/bin/aarch64-none-elf-gcc-ar" \
    -DBUILD_SHARED_LIBS:BOOL=OFF \
    -DOPENGL_USE_EGL:BOOL=ON \
    -DOPENGL_opengl_LIBRARY="${PORTLIBS_PREFIX}/lib" \
    .
  
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="$pkgdir" install
  
  install -Dm644 COPYING "$pkgdir"/opt/devkitpro/portlibs/switch/licenses/$pkgname/COPYING
}

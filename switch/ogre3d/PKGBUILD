# Maintainer: COMP Omega <compomega@tutanota.com>
pkgname=switch-ogre
pkgver=1.12.5
pkgrel=1
pkgdesc='scene-oriented, flexible 3D engine written in C++'
arch=('any')
url='https://www.ogre3d.org/'
license=('MIT')
options=(!strip libtool staticlibs)
source=( "https://github.com/OGRECave/ogre/archive/v${pkgver}.tar.gz" "ogre-switch.patch")
makedepends=('devkitA64' 'devkitpro-pkgbuild-helpers')
depends=('switch-zlib' 'switch-zziplib' 'switch-freetype' 'switch-freeimage' 'switch-pugixml')
sha256sums=(
  'f14465012b0a10bc3fd3669d02965279878b1048e9972c730866e64688a9ec8d'
  '5c37201788ede975fe755cee5c04e940d7ba7cdd7fc20245d518438f55bfeaa7')
groups=('switch-portlibs')
build() {
  cd ogre-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -Np1 -i "$srcdir"/ogre-switch.patch

  cmake -G"Unix Makefiles" \
    -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/switch.cmake \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DOGRE_ENABLE_PRECOMPILED_HEADERS=FALSE \
    -DOGRE_BUILD_COMPONENT_OVERLAY_IMGUI=OFF \
    -DSWITCH=ON -DOGRE_STATIC=ON -DOGRE_BUILD_DEPENDENCIES=OFF \
    -DOGRE_BUILD_RENDERSYSTEM_GL=OFF \
    -DOGRE_BUILD_RENDERSYSTEM_GL3PLUS=ON \
    -DOGRE_BUILD_TOOLS=OFF -DOGRE_BUILD_TESTS=OFF \
    -DOGRE_INSTALL_DOCS=OFF -DOGRE_BUILD_SAMPLES=OFF \
    -DOPENGL_gl_LIBRARY=/opt/devkitpro/portlibs/switch/lib/libglapi.a \
    -DOPENGL_INCLUDE_DIR=/opt/devkitpro/portlibs/switch/include \
    .

  make

}

package() {

  cd ogre-$pkgver

  source /opt/devkitpro/switchvars.sh

  make install DESTDIR="$pkgdir"

}

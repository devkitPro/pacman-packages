# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.37.0
pkgrel=1
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch")
sha256sums=(
  '1d2d4adbaf048a2fa6ee134575032c4b2dad9a7efafd5b3e69b88db935afaddf'
  'cc35477079605ee56afc35b55b963c068fc1dfe4d0b88c8ebb65c9f71535051f'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-sdl2' 'switch-ffmpeg' 'switch-mesa' 'switch-libplacebo')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
}

build() {
  cd mpv-$pkgver
  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt \
    -Dlibmpv=true -Dcplayer=false -Dtests=false \
    -Dplain-gl=enabled -Dsdl2=enabled -Dsdl2-gamepad=enabled -Dsdl2-video=enabled \
    -Dlibarchive=disabled -Dlua=disabled -Djpeg=disabled -Diconv=disabled \
    -Dsdl2-audio=enabled build

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}
# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.36.0
pkgrel=3
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch")
sha256sums=(
  '29abc44f8ebee013bb2f9fe14d80b30db19b534c679056e4851ceadf5a5e8bf6'
  'afd7a950bf93cc417a7b46a6df40870ed6d0f7854feb2d4d64630d9769141a7a'
)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=('switch-sdl2' 'switch-ffmpeg' 'switch-mesa')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
}

build() {
  cd mpv-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./bootstrap.py
  export CFLAGS="$CFLAGS -D_POSIX_VERSION=200809L -I$srcdir/mpv-${pkgver}/osdep/switch"
  TARGET=aarch64-none-elf ./waf configure --prefix="${PORTLIBS_PREFIX}" \
    --disable-libmpv-shared --enable-libmpv-static --disable-cplayer \
    --disable-iconv --disable-jpeg --disable-libavdevice \
    --enable-sdl2 --enable-sdl2-audio --enable-sdl2-gamepad --enable-sdl2-video
  sed -i 's/#define HAVE_POSIX 1/#define HAVE_POSIX 0/' build/config.h
  ./waf build
}

package() {
  cd mpv-$pkgver

  ./waf install --destdir=$pkgdir
}

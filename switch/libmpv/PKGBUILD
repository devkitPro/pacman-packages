# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: averne <averne381@gmail.com>
pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.41.0
pkgrel=1
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch")
sha256sums=(
  'ee21092a5ee427353392360929dc64645c54479aefdb5babc5cfbb5fad626209'
  'eca661a8851beee53540cb360e332ec240ee24a0557231ef849a4c790c309b57'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-sdl2' 'switch-ffmpeg' 'switch-mesa' 'switch-libarchive' 'switch-liblua51' 'switch-libplacebo')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
}

build() {
  cd mpv-$pkgver

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt build \
    -Dlibmpv=true -Dcplayer=false -Dtests=false -Dplain-gl=enabled -Dlua=disabled \
    -Dsdl2-gamepad=enabled -Dsdl2-audio=enabled -Dsdl2-video=enabled \
    -Diconv=disabled

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}

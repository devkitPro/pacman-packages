# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
pkgbasename=libmpv
pkgname=switch-${pkgbasename}
pkgver=0.36.0
pkgrel=4
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("${pkgbasename}-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch" "lavfi.patch")
sha256sums=(
  '29abc44f8ebee013bb2f9fe14d80b30db19b534c679056e4851ceadf5a5e8bf6'
  'fb2f68a3435d85382e0b9261751b1a1624864f4be53ae408113574acd8522a8a'
  'd24aa173efcddab768db23ba44d51fba59fcdb2d511f6ce3a028aca2b7e3deea'
)
makedepends=("dkp-toolchain-vars" "dkp-meson-scripts")
depends=('switch-sdl2' 'switch-ffmpeg' 'switch-mesa' 'switch-libarchive' 'switch-liblua51')
groups=('switch-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
  patch -Np1 -i "$srcdir/lavfi.patch"
}

build() {
  cd mpv-$pkgver

  /opt/devkitpro/meson-cross.sh switch ../crossfile.txt build \
    -Dlibmpv=true -Dcplayer=false -Dtests=false -Dplain-gl=enabled -Dlua=enabled \
    -Dsdl2=enabled -Dsdl2-gamepad=enabled -Dsdl2-audio=enabled -Dsdl2-video=enabled \
    -Dlibplacebo=disabled -Dlibavdevice=disabled -Djpeg=disabled -Diconv=disabled

  meson compile -C build
}

package() {
  cd mpv-$pkgver

  DESTDIR="$pkgdir" meson install -C build
}

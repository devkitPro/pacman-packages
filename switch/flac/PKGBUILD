# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>

pkgname=switch-flac
pkgver=1.3.2
pkgrel=1
pkgdesc='FLAC stands for Free Lossless Audio Codec, an audio format similar to MP3, but lossless...'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
depends=("switch-libogg")
source=("https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz")
sha256sums=('91cfc3ed61dc40f47f050a109b08610667d73477af6ef36dcad31c31a4a8d53f')
makedepends=('devkitpro-pkgbuild-helpers')

build() {
  cd flac-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static --disable-thorough-tests \
    --disable-xmms-plugin --disable-oggtest --disable-cpplibs \
    --disable-largefile

  make
}

package() {
  cd flac-$pkgver

  make DESTDIR="$pkgdir" install
}


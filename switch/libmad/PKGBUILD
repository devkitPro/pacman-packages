# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=switch-libmad
pkgver=0.15.1b
pkgrel=2
pkgdesc='A high-quality MPEG audio decoder. (for Nintendo 3DS homebrew development)'
arch=('any')
url='https://www.underbit.com'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://sourceforge.net/projects/mad/files/libmad/${pkgver}/libmad-${pkgver}.tar.gz")
sha256sums=(
 'bbfac3ed6bfbc2823d3775ebb931087371e142bb0e9bb1bee51a76a6e0078690'
)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
groups=('switch-portlibs')

prepare() {
  cd ${srcdir}/libmad-${pkgver}
  sed -i 's,optimize="$optimize -fforce-mem",#optimize="$optimize -fforce-mem",'  configure.ac
  # configury is too old for aarch64-none-elf
  touch NEWS AUTHORS ChangeLog
  autoreconf -fi
  automake --foreign --add-missing --copy --force-missing
}

build() {
  cd libmad-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf --disable-shared --enable-static --with-pic=yes
  make
}

package() {
  cd libmad-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  make DESTDIR="$pkgdir" install
}

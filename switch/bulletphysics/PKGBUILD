
# Maintainer: fincs <fincs.alt1@gmail.com>
pkgname=switch-bulletphysics
pkgver=2.88
pkgrel=3
pkgdesc='Bullet Physics SDK: real-time collision detection and multi-physics simulation for VR, games, visual effects, robotics, machine learning etc.'
arch=('any')
url='http://bulletphysics.org/'
license=('apache')
options=(!strip libtool staticlibs)
source=( "https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz" "bulletphysics.patch")
makedepends=('devkitA64' 'dkp-toolchain-vars')
sha256sums=(
  '21c135775527754fc2929db1db5144e92ad0218ae72840a9f162acb467a7bbf9'
  '03735450ca1109ba95c692453049d8ae42b71731247c1623dcebf564287885d7')
groups=('switch-portlibs')
build() {
  cd bullet3-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  patch -Np1 -i "$srcdir"/bulletphysics.patch

  aarch64-none-elf-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CFLAGS" \
    -DCMAKE_AR="$DEVKITPRO/devkitA64/bin/aarch64-none-elf-gcc-ar" \
    -DBUILD_CPU_DEMOS:BOOL=OFF -DBUILD_BULLET3:BOOL=OFF -DBUILD_OPENGL3_DEMOS:BOOL=OFF -DBUILD_BULLET2_DEMOS:BOOL=OFF -DBUILD_EXTRAS:BOOL=OFF -DBUILD_UNIT_TESTS:BOOL=OFF \
    .

  make

}

package() {

  cd bullet3-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  make install DESTDIR="$pkgdir"

}

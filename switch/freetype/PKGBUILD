# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>
# Contributor: Pablo Curiel <pabloacurielz@gmail.com>

pkgname=switch-freetype
pkgver=2.13.2
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
depends=("switch-bzip2"
         "switch-libpng"
         "switch-zlib"
         "switch-harfbuzz"
)
groups=('switch-portlibs')
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.xz")

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=aarch64-none-elf \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --with-png \
    --enable-freetype-config

  make
}

package() {
  cd freetype-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # patch the config script to always output static libraries when not relying on pkg-config
  sed 's/\$show_static/yes/' -i "${pkgdir}/${PORTLIBS_PREFIX}/bin/freetype-config"

  # Licenses
  install -Dm644 "LICENSE.TXT" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/FTL.TXT"

  # remove useless stuff
  rm -r "${pkgdir}/${PORTLIBS_PREFIX}/share"
}
sha256sums=('12991c4e55c506dd7f9b765933e62fd2be2e06d421505d7950a132e4f1bb484d')

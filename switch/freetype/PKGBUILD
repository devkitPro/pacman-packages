# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Aaron Lindsay <aaron@aaronlindsay.com>

pkgname=switch-freetype
pkgver=2.9
pkgrel=1
pkgdesc='FreeType is a freely available software library to render fonts.'
arch=('any')
url='https://www.freetype.org/'
license=(GPL2+ custom:FreeType)
options=(!strip libtool staticlibs)
depends=("switch-bzip2"
         "switch-libpng"
         "switch-zlib")
source=("http://download.savannah.gnu.org/releases/freetype/freetype-$pkgver.tar.gz")
sha256sums=('bf380e4d7c4f3b5b1c1a7b2bf3abb967bda5e9ab480d0df656e0e08c5019c5e6')

build() {
  cd freetype-$pkgver

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  ./configure --prefix="$DEVKITPRO/portlibs/switch" --host=aarch64-none-elf \
    --disable-shared --enable-static \
    --with-zlib \
    --with-bzip2 \
    --with-png

  make
}

package() {
  cd freetype-$pkgver

  make DESTDIR="$pkgdir" install
  
  # Licenses
  install -Dm644 "docs/LICENSE.TXT" "${pkgdir}${DEVKITPRO}/portlibs/switch/licenses/${pkgname}/LICENSE.TXT"
  install -Dm644 "docs/GPLv2.TXT"   "${pkgdir}${DEVKITPRO}/portlibs/switch/licenses/${pkgname}/GPLv2.TXT"
  install -Dm644 "docs/FTL.TXT"     "${pkgdir}${DEVKITPRO}/portlibs/switch/licenses/${pkgname}/FTL.TXT"
  
  # remove useless stuff
  rm -r "${pkgdir}${DEVKITPRO}/portlibs/switch/share"
}

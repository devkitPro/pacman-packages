# Maintainer: Ezekiel Bethel <stary@9net.org>

pkgname=switch-libpcre2
pkgver=10.33
pkgrel=1
pkgdesc='libpcre2 port (for Nintendo Switch homebrew development)'
arch=('any')
url='https://www.pcre.org/'
license=('BSD')
options=(!strip libtool staticlibs)
depends=()
source=("https://ftp.pcre.org/pub/pcre/pcre2-${pkgver}.tar.gz"
)

sha256sums=('e2e2899a97489fc6ad1b0cc3da7952c7cca991b4a0f7db6649b75d9721025d31')

build() {
  cd pcre2-$pkgver

  source /opt/devkitpro/switchvars.sh

  # Disable building programs & tests.
  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in
  sed -i 's/^TESTS = .*//' Makefile.in
  sed -i 's/^noinst_PROGRAMS = .*//' Makefile.in

  ./configure --prefix=$PORTLIBS_PREFIX --host=aarch64-none-elf \
    --disable-shared --enable-static --enable-pcre2-16 --enable-pcre2-32

  make
}

package() {
  cd pcre2-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install
  # Delete the docs
  rm -r $pkgdir/$PORTLIBS_PREFIX/share/
}

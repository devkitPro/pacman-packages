# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>
# Contributor: giroletm <rsmromaji@gmail.com>

pkgname=switch-sdl2_image
pkgver=2.6.3
pkgrel=1
pkgdesc="SDL2 image loading library (Nintendo Switch port)."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=(
  'switch-sdl2'
  'switch-libpng'
  'switch-libjpeg-turbo'
  'switch-libwebp'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz" "Makefile.in.patch")
sha256sums=(
  '931c9be5bf1d7c8fae9b7dc157828b7eee874e23c7f24b44ba7eff6b4836312c'
  'bf19fda146f372d1b308637e4bd16a4dc338c4d262f08f8f172ee8af2e729702'
)
groups=('switch-portlibs' 'switch-sdl2-libs')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  patch -Np1 -i "$srcdir"/Makefile.in.patch Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg libwebp" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  install -Dm644 "LICENSE.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/LICENSE.txt"
}

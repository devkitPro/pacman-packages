# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>

pkgname=switch-sdl2_image
pkgver=2.0.3
pkgrel=5
pkgdesc="SDL2 image loading library (Nintendo Switch port)."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=(
  'switch-sdl2'
  'switch-libpng'
  'switch-libjpeg-turbo'
  'switch-libwebp'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz" "Makefile.in.patch")
sha256sums=(
  '3510c25da735ffcd8ce3b65073150ff4f7f9493b866e85b83738083b556d2368'
  '58b18bccfddf42d44163a461a2e0b36fa66204cc7dc0b57650bb0087aa0a3b1c'
)
groups=('switch-portlibs' 'switch-sdl2')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  patch -Np1 -i "$srcdir"/Makefile.in.patch Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg libwebp" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}

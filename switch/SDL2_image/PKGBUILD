# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>

pkgname=switch-sdl2_image
pkgver=2.0.4
pkgrel=1
pkgdesc="SDL2 image loading library (Nintendo Switch port)."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'dkp-toolchain-vars')
depends=(
  'switch-sdl2'
  'switch-libpng'
  'switch-libjpeg-turbo'
  'switch-libwebp'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz" "Makefile.in.patch")
sha256sums=(
  'e74ec49c2402eb242fbfa16f2f43a19582a74c2eabfbfb873f00d4250038ceac'
  'c15da8e087e7ca4ceef3dcce8501b21cbe6569563c345cf2a1591f20316d8d86'
)
groups=('switch-portlibs' 'switch-sdl2-libs')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  patch -Np1 -i "$srcdir"/Makefile.in.patch Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg libwebp" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}

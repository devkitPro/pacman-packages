# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Julian Uy <uyjulian@gmail.com>

pkgname=switch-mupdf
pkgver=1.17.0
pkgrel=1
pkgdesc='MuPDF is a lightweight PDF, XPS, and E-book viewer.'
arch=('any')
url='https://www.mupdf.com/'
license=('AGPL')
options=(!strip libtool staticlibs)
depends=("switch-freetype"
         "switch-libjpeg-turbo"
         "switch-zlib"
)

makedepends=('dkp-toolchain-vars')

source=("https://www.mupdf.com/downloads/archive/mupdf-$pkgver-source.tar.xz" "mupdf.patch")

groups=('switch-portlibs')

sha256sums=(
  'c935fb2593d9a28d9b56b59dad6e3b0716a6790f8a257a68fa7dcb4430bc6086'
  '5641105980d95c2cb77cf053a13d2255c422767b5c9ec4c2a13c1aa14d1e18bb'
)

build() {
  cd mupdf-$pkgver-source

  patch --strip=1 --input=$srcdir/mupdf.patch

  make generate

  source /opt/devkitpro/switchvars.sh

  make build=release \
    OS=switch \
    libs
}

package() {
  cd mupdf-$pkgver-source
  make build=release \
    prefix="${PORTLIBS_PREFIX}" DESTDIR="$pkgdir" \
    OS=switch \
    TOOL_APPS=/dev/null \
    VIEW_APPS= \
    install
}

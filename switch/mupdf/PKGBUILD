# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Julian Uy <uyjulian@gmail.com>

pkgname=switch-mupdf
pkgver=1.24.11
pkgrel=1
pkgdesc='MuPDF is a lightweight PDF, XPS, and E-book viewer.'
arch=('any')
url='https://www.mupdf.com/'
license=('AGPL')
options=(!strip libtool staticlibs)
depends=("switch-freetype"
         "switch-harfbuzz"
         "switch-libjpeg-turbo"
         "switch-zlib"
)

makedepends=('switch-pkg-config' 'dkp-toolchain-vars')

source=("https://casper.mupdf.com/downloads/archive/mupdf-$pkgver-source.tar.lz" "mupdf.patch")

groups=('switch-portlibs')

sha256sums=(
    'ac159293040934e250e4495782f8cd04c8cbbaf992fbd9faf32dab2ec657117b'
    '396a8350670e363adabe12485e3ee32b695c46b7a97e9acd50a0816fc110eabf'
)

build() {
  cd mupdf-$pkgver-source

  patch --strip=1 --input=$srcdir/mupdf.patch

  make generate

  source /opt/devkitpro/switchvars.sh

  make build=release \
    OS=switch \
    libs
}

package() {
  cd mupdf-$pkgver-source
  make build=release \
    prefix="${PORTLIBS_PREFIX}" DESTDIR="$pkgdir" \
    OS=switch \
    install-libs
}

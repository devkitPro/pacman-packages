# Maintainer:  Dave Murphy <davem@devkitpro.org>
# Contributor: Cpasjuste <cpasjuste@gmail.com>
# Contributor: carstene1ns <dev f4ke de>

pkgname=switch-sdl2_mixer
pkgver=2.0.2
pkgrel=3
pkgdesc="A sample multi-channel audio mixer library (Nintendo Switch port)."
arch=('any')
url="https://libsdl.org/projects/SDL_mixer/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=(
  'switch-sdl2'
  'switch-libvorbisidec'
  'switch-libmodplug'
  'switch-mpg123'
)
source=("${url}release/SDL2_mixer-${pkgver}.tar.gz")
sha256sums=('4e615e27efca4f439df9af6aa2c6de84150d17cbfd12174b54868c12f19c83bb')
groups=('switch-portlibs' 'switch-sdl2')

build() {
  cd SDL2_mixer-$pkgver

  source ${DEVKITPRO}/switchvars.sh

  # patch out compiling playwave and playmus
  sed 's|\$(objects)/play.*mus\$(EXE)||' -i Makefile.in

  LIBS="-lm" ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=aarch64-none-elf --disable-shared --enable-static \
    --disable-music-cmd \
    --enable-music-ogg-tremor \
    --enable-music-mod-modplug

  make
}

package() {
  cd SDL2_mixer-$pkgver

  source /opt/devkitpro/switchvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: vorbisidec libmodplug libmpg123" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_mixer.pc"

  # License
  install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}

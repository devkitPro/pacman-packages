
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=nds-zlib
pkgver=1.2.11
pkgrel=1
pkgdesc='Deflate compression method library (for Nintendo DS homebrew development)'
arch=('any')
url='http://www.zlib.net/'
license=('zlib')
options=(!strip libtool staticlibs)
source=("http://downloads.devkitpro.org/packages/sources/zlib-$pkgver.tar.xz")
sha256sums=('4ff941449631ace0d4d203e3483be9dbc9da454084111f97ea0a2114e19bf066')
makedepends=('devkitpro-pkgbuild-helpers')
groups=('nds-portlibs')

build() {
  cd zlib-$pkgver

  source /opt/devkitpro/ndsvars.sh

  CHOST=arm-none-eabi \
    ./configure --prefix="${PORTLIBS_PREFIX}" --static
  make libz.a # only build library, no programs


  # libminizip
  cd contrib/minizip
  autoreconf --force --verbose --install
  CFLAGS="${CFLAGS} -DUSE_FILE32API"
  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd zlib-$pkgver

  source /opt/devkitpro/ndsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -d "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname
  tail -n 31 README > "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share

  # libminizip
  cd contrib/minizip
  make DESTDIR="$pkgdir" install
}

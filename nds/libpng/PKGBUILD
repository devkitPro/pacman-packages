# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=nds-libpng
pkgver=1.6.37
pkgrel=1
pkgdesc='PNG format graphic files library (for Nintendo NDS homebrew development)'
arch=('any')
url='http://www.libpng.org'
license=('custom')
options=(!strip libtool staticlibs)
depends=('nds-zlib')
groups=('nds-portlibs')
makedepends=('nds-pkg-config' 'devkitpro-pkgbuild-helpers')
groups=('nds-portlibs')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=('505e70834d35383537b6491e7ae8641f1a4bed1876dbfe361201fc80868d88ca')

build() {
  cd libpng-$pkgver

  source /opt/devkitpro/ndsvars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make
}

package() {
  cd libpng-$pkgver

  source /opt/devkitpro/ndsvars.sh

  make LN_S=cp DESTDIR="$pkgdir" install
  # license
  install -Dm644 LICENSE "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/LICENSE
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}

# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=armv4t-libconfig
pkgver=1.7.2
pkgrel=1
pkgdesc='C/C++ Configuration File Library (for armv4t homebrew development)'
arch=('any')
url="https://www.hyperrealm.com/libconfig/libconfig.html"
license=('LGPL')
options=(!strip libtool staticlibs)
source=("https://hyperrealm.github.io/libconfig/dist/libconfig-${pkgver}.tar.gz" "libconfig.patch")
makedepends=('dkp-toolchain-vars')
sha256sums=(
  '7c3c7a9c73ff3302084386e96f903eb62ce06953bb1666235fac74363a16fad9'
  '474c17940200d4aa286e486568a0ecaba2130a758f108e2a0b86c6fed5915d1f'
)
groups=('armv4t-portlibs')

build() {
  cd libconfig-$pkgver

  source /opt/devkitpro/armv4tvars.sh
  
  patch -Np1 -i "$srcdir"/libconfig.patch

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
     --disable-shared --enable-static \
     --disable-examples

  make
}

package() {
  cd libconfig-$pkgver

  source /opt/devkitpro/armv4tvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 COPYING.LIB "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING.LIB
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
}


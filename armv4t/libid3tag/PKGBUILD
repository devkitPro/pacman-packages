
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=dkp-armv4t-libid3tag
pkgver=0.15.1b
pkgrel=1
pkgdesc='A library for reading and (eventually) writing ID3 tags, both ID3v1 and the various versions of ID3v2. (for armv4t homebrew development)'
arch=('any')
url='https://www.underbit.com'
license=('GPL')
options=(!strip libtool staticlibs)
source=("https://sourceforge.net/projects/mad/files/libid3tag/0.15.1b/libid3tag-${pkgver}.tar.gz")
groups=('dkp-armv4t-portlibs')
sha256sums=('63da4f6e7997278f8a3fef4c6a372d342f705051d1eeb6a46a86b03610e26151')
depends=('dkp-armv4t-zlib')
makedepends=('devkitpro-pkgbuild-helpers')

build() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/armv4tvars.sh

  CPPFLAGS="${CFLAGS} ${CPPFLAGS}" ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi --disable-shared --enable-static
  make
}

package() {
  cd libid3tag-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/armv4tvars.sh

  make DESTDIR="$pkgdir" install
}


# Maintainer: Dave Murphy <davem@devkitpro.org>

_realname=bzip2
pkgname=armv4t-${_realname}
pkgver=1.0.9
pkgrel=2
pkgdesc="A high-quality data compression program"
arch=('any')

url="https://github.com/devkitPro/bzip2"
license=(bzip2)
options=('strip' 'staticlibs')

source=("${url}/releases/download/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.gz")
sha256sums=('4f51c50c8da4bb48c81ca05fe02e8e44657402ad4bdfd2957eaf421bb43f5b7f')

makedepends=('devkitpro-pkgbuild-helpers')
groups=('armv4t-portlibs')

options=(!strip staticlibs)

_prefix=/opt/devkitpro/crosstools

build() {

  source /opt/devkitpro/armv4tvars.sh

  cd ${_realname}-${pkgver}

  ./configure --prefix="${PORTLIBS_PREFIX}" --host=arm-none-eabi \
    --disable-shared --enable-static
  make all-libs
}

package() {

  source /opt/devkitpro/armv4tvars.sh

  cd ${_realname}-${pkgver}

  install -Dm 644 "$srcdir/${_realname}-${pkgver}"/LICENSE -t "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}"
  install -Dm 644 "$srcdir/${_realname}-${pkgver}"/bzlib.h -t "${pkgdir}/${PORTLIBS_PREFIX}/include"
  install -Dm 644 "$srcdir/${_realname}-${pkgver}"/libbz2.a -t "${pkgdir}/${PORTLIBS_PREFIX}/lib"
  install -Dm 644 "$srcdir/${_realname}-${pkgver}"/bzip2.pc -t "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig"

}


